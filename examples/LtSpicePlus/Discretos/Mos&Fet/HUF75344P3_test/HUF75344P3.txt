.SUBCKT HUF75344p3 2 1 3
*Spice model for HUF75344P3, HUF75344G3 & HUF75344S3S
*rev 3Feb98
 
CA 12 8 4.9e-9
CB 15 14 4.75e-9
CIN 6 8 2.85e-9
 
DBODY 7 5 DBODYMOD
DBREAK 5 11 DBREAKMOD
DPLCAP 10 5 DPLCAPMOD
 
EBREAK 11 7 17 18 59.7
EDS 14 8 5 8 1
EGS 13 8 6 8 1
ESG 6 10 6 8 1
EVTHRES 6 21 19 8 1
EVTEMP 20 6 18 22 1
 
IT 8 17 1
 
LGATE 1 9 2.6e-9
LDRAIN 2 5 1e-9
LSOURCE 3 7 1.1e-9
 
RLGATE 1 9 26
RLDRAIN 2 5 10
RLSOURCE 3 7 11
 
MMED 16 6 8 8 MMEDMOD
MSTRO 16 6 8 8 MSTROMOD
MWEAK 16 21 8 8 MWEAKMOD
 
RBREAK 17 18 RBREAKMOD 1
RDRAIN 50 16 RDRAINMOD 1.94e-3
RGATE 9 20 0.36
RSLC1 5 51 RSLCMOD 1e-6
RSLC2 5 50 1e3
RSOURCE 8 7 RSOURCEMOD 3.5e-3
RVTHRES 22 8 RVTHRESMOD 1
RVTEMP 18 19 RVTEMPMOD 1
 
S1A 6 12 13 8 S1AMOD
S1B 13 12 13 8 S1BMOD
S2A 6 15 14 13 S2AMOD
S2B 13 15 14 13 S2BMOD
 
VBAT 22 19 DC 1
 
ESLC 51 50 VALUE={(V(5,51)/ABS(V(5,51)))*(PWR(V(5,51)/(1e-6*400),3))}
 
.MODEL DBODYMOD D (IS=2.95e-12 RS=2.6e-3 TRS1=1.05e-3 TRS2=5e-7
+CJO=5.19e-9 TT=5.9e-8 M=0.55)
.MODEL DBREAKMOD D (RS=1.65e-1 IKF=30 TRS1=1.15e-4 TRS2=2.27e-6)
.MODEL DPLCAPMOD D (CJO=5.40e-9 IS=1e-30 N=1 M=0.88 )
 
.MODEL MMEDMOD NMOS (VTO=3.29 KP=5.5 IS=1e-30 N=10 TOX=1 L=1u W=1u RG=.36)
.MODEL MSTROMOD NMOS (VTO=3.83 KP=123 IS=1e-30 N=10 TOX=1 L=1u W=1u)
.MODEL MWEAKMOD NMOS (VTO=2.9 KP=0.04 IS=1e-30 N=10 TOX=1 L=1u W=1u RG=3.6)
 
.MODEL RBREAKMOD RES (TC1=1.15e-3 TC2=2e-7)
.MODEL RDRAINMOD RES (TC1=1.37e-2 TC2=3.85e-5)
.MODEL RSLCMOD RES (TC1=1.45e-4 TC2=2.11e-6)
.MODEL RSOURCEMOD RES (TC1=0 TC2=0)
.MODEL RVTHRESMOD RES (TC1=-3.7e-3 TC2=-1.6e-5)
.MODEL RVTEMPMOD RES (TC1=-2.4e-3 TC2=7e-7)
 
.MODEL S1AMOD VSWITCH (RON=1e-5 ROFF=0.1 VON=-6.9 VOFF= -3.9)
.MODEL S1BMOD VSWITCH (RON=1e-5 ROFF=0.1 VON=-3.9 VOFF= -6.9)
.MODEL S2AMOD VSWITCH (RON=1e-5 ROFF=0.1 VON=-2.99 VOFF= 2.39)
.MODEL S2BMOD VSWITCH (RON=1e-5 ROFF=0.1 VON=2.39 VOFF= -2.99)
.ENDS
 
.SUBCKT huf75344p3_thermal TH TL
*Thermal Model Subcircuit
*5Feb99
 
CTHERM1 TH c2 5.0e-3
CTHERM2 c2 c3 1.0e-2
CTHERM3 c3 c4 1.30E-2
CTHERM4 c4 c5 1.5E-2
CTHERM5 c5 c6 2.2E-2
CTHERM6 c6 TL 8.5e-2
 
RTHERM1 TH c2 6.0E-4
RTHERM2 c2 c3 3.5e-3
RTHERM3 c3 c4 2.5e-2
RTHERM4 c4 c5 4.8e-2
RTHERM5 c5 c6 1.6e-1
RTHERM6 c6 TL 1.8e-1
.ENDS