Version 4
SHEET 1 2224 2104
WIRE -368 400 -368 368
WIRE -368 528 -368 480
WIRE -368 560 -368 528
WIRE -224 368 -368 368
WIRE -176 288 -176 256
WIRE -176 528 -368 528
WIRE -176 528 -176 384
WIRE 48 256 -176 256
WIRE 48 304 48 256
WIRE 48 528 -176 528
WIRE 48 528 48 384
FLAG -368 560 0
SYMBOL voltage -368 384 R0
SYMATTR InstName V1
SYMATTR Value 0
SYMBOL nmos -224 288 R0
SYMATTR InstName M1
SYMATTR Value FDB2532
SYMATTR Prefix X
SYMBOL voltage 48 288 R0
SYMATTR InstName V2
SYMATTR Value 5
TEXT -344 128 Left 0 !.dc V2 -0.7 9 0.01 V1 2 5 1
TEXT -416 728 Left 0 ;1. Add a "nmos" component to the schematic.\n2. Move the cursor over nmos-transistor and \n then press <Ctrl>+ the right mouse button.\n3. Replace Prefix: MN   with   Prefix: X\n Prefix X is needed for subcircuits(.SUBCKT ..).\n4. OK\n5. Replace NMOS with FDB2532 on your transistor in the schematic.\n6. Put the model either in a file or on the schematic.\nAdd the line  \n    .include FDB32.mod\nif you use a model file. The model file should be in\nthe same folder as the schematic or in the ...\\sub\nfolder of the LTspice installation.
TEXT -344 160 Left 0 ;.include FDB2532.mod
TEXT 384 -80 Left 0 !.SUBCKT FDB2532 2 1 3      \n*Spice model for FDB3532, FDP2532 & FDI2532\n*Nom Temp=25 deg C \n*April 2002\n \nCa 12 8 1.4e-9\nCb 15 14 1.6e-9\nCin 6 8 5.61e-9\n \nDbody 7 5 DbodyMOD\nDbreak 5 11 DbreakMOD\nDplcap 10 5 DplcapMOD\n \nEbreak 11 7 17 18 159\nEds 14 8 5 8 1\nEgs 13 8 6 8 1\nEsg 6 10 6 8 1\nEvthres 6 21 19 8 1\nEvtemp 20 6 18 22 1\n \nIt 8 17 1\n \nLgate 1 9 9.56e-9\nLdrain 2 5 1.0e-9\nLsource 3 7 7.71e-9\n \nRLgate 1 9 95.6\nRLdrain 2 5 10\nRLsource 3 7 77.1\n \nMmed 16 6 8 8 MmedMOD\nMstro 16 6 8 8 MstroMOD \nMweak 16 21 8 8 MweakMOD    \n \nRbreak 17 18 RbreakMOD 1\nRdrain 50 16 RdrainMOD 9.6e-3\nRgate 9 20 1.01\nRSLC1 5 51 RSLCMOD 1.0e-6\nRSLC2 5 50 1.0e3\nRsource 8 7 RsourceMOD 3.0e-3\nRvthres 22 8 RvthresMOD 1\nRvtemp 18 19 RvtempMOD 1\nS1a 6 12 13 8 S1AMOD\nS1b 13 12 13 8 S1BMOD\nS2a 6 15 14 13 S2AMOD\nS2b 13 15 14 13 S2BMOD\n \nVbat 22 19 DC 1\n \nESLC 51 50  VALUE={(V(5,51)/ABS(V(5,51)))*(PWR(V(5,51)/(1e-6*190),3))}\n \n.MODEL DbodyMOD D (IS=6.0E-11 N=1.09 RS=2.3e-3 TRS1=3.0e-3 TRS2=1.0e-6\n+ CJO=3.9e-9 M=0.65 TT=4.8e-8 XTI=4.2)\n.MODEL DbreakMOD D (RS=0.17 TRS1=3.0e-3 TRS2=-8.9e-6)\n.MODEL DplcapMOD D (CJO=1.0e-9 IS=1.0e-30 N=10 M=0.6)\n \n.MODEL MmedMOD NMOS (VTO=3.55 KP=10 IS=1e-30 N=10 TOX=1 L=1u W=1u RG=1.01)\n.MODEL MstroMOD NMOS (VTO=4.2 KP=145 IS=1e-30 N=10 TOX=1 L=1u W=1u)\n.MODEL MweakMOD NMOS (VTO=2.9 KP=0.05 IS=1e-30 N=10 TOX=1 L=1u W=1u RG=10.1 RS=0.1) \n \n.MODEL RbreakMOD RES (TC1=1.1e-3 TC2=-9.0e-7)\n.MODEL RdrainMOD RES (TC1=9.0e-3 TC2=3.5e-5)\n.MODEL RSLCMOD RES (TC1=3.4e-3 TC2=1.5e-6)\n.MODEL RsourceMOD RES (TC1=4.0e-3 TC2=1.0e-6)\n.MODEL RvthresMOD RES (TC1=-4.1e-3 TC2=-1.4e-5)\n.MODEL RvtempMOD RES (TC1=-4.0e-3 TC2=3.5e-6)\n \n.MODEL S1AMOD VSWITCH (RON=1e-5 ROFF=0.1 VON=-6.0 VOFF=-4.0)\n.MODEL S1BMOD VSWITCH (RON=1e-5 ROFF=0.1 VON=-4.0 VOFF=-6.0)\n.MODEL S2AMOD VSWITCH (RON=1e-5 ROFF=0.1 VON=-1.4 VOFF=1.0)\n.MODEL S2BMOD VSWITCH (RON=1e-5 ROFF=0.1 VON=1.0 VOFF=-1.4)\n \n.ENDS
TEXT -328 -56 Left 0 ;Use the "Alternate" solver.\nControl Panel -> SPICE > Solver:Alternate\n \nPlot Ix(M1:D)
