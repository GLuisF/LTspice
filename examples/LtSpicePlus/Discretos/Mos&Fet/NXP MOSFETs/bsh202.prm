.SUBCKT BSH202 1 2 3
* 1=drain  2=gate  3=source
Cgs  6 3 150e-12
Cgd1 6 4 122e-12
Cgd2 1 4 9e-12
M1 5 6 3 3 MOST1
M2 4 6 5 3 MOST2
D1 1 3 Dbody
Rd 5 1 Rtemp 525e-3
Rg 2 6 170
.MODEL MOST1 PMOS(Level=3 W=0.1 L=0.3e-6 Vto=-1.69 Kp=0.184e-5
+                Rs=0 Rd=0 Uo=650 Vmax=0 Xj=5e-7 Kappa=0.08 
+                Eta=2e-4 Tpg=-1 Is=0 Ld=0 Wd=0 Cgso=0 Cgdo=0
+                Cgbo=0 Nfs=2.2e12 Delta=0.1 Theta=0)
.MODEL MOST2 PMOS(Level=3 W=0.1 L=0.3e-6 Vto=1.86 Kp=0.184e-5
+                Rs=1000 Rd=1000 Uo=650 Vmax=0 Xj=5e-7 Kappa=0.08
+                Eta=2e-4 Tpg=-1 Is=0 Ld=0 Wd=0 Cgso=0 Cgdo=0
+                Cgbo=0 Nfs=0 Delta=0.1 Theta=0)
.MODEL Dbody D(Is=1e-14 N=0.7 Rs=550e-3 Ikf=1e3 Cjo=0 M=0.3 Vj=0.3
+                Bv=30 Ibv=10e-6 Tt=45e-9 Fc=0.5)
.MODEL Rtemp RES(TC1=4.6e-3 TC2=8e-6)
.ENDS