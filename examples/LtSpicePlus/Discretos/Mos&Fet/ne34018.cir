* FILENAME:         NE34018.CIR
* NEC PART NUMBER:  NE34018
* LAST MODIFIED:    12/97
* BIAS CONDITIONS:  Vds=1V to 3V, Id=5mA to 40mA
* FREQ RANGE:       0.5GHz to 6ghz

* TOM GaAsFET Model
*
*                             CGDpkg
*                    ,-------||--------------. 
*                    |                       |
*                    |                       |          DRAIN 
*   GATE             |       |----o-Rdx-LD---o---LDpkg---o
*   o-------LGpkg----o-LG----|Q1  |          |         
*                    |       |--  RDB        |         
*                    |         |  |          |         
*                    |         |  CBS        |         
*                    |         |__|          |         
*                    |         |             |         
*                    |         Rsx           --         
*             CGSpkg--         |             -- CDSpkg  
*                   --         LS            |     
*                    |         |             |   
*                    '---------o-------------'     
*                              |
*                              |
*                             LSpkg
*                              | 
*                              o SOURCE 
*           
*       LG=0.93nH           LGpkg=0.18nH
*       LS=0.25nH           LSpkg=0.09nH
*       LD=1.25nH           LDpkg=0.18nH
*       RDB=650 ohms        CGDpkg=0.02pF
*       CBS=10pF            CGSpkg=0.1pF
*       Rdx=1.5 ohms        CDSpkg=0.1pF
*       Rsx=2 ohms
*                   d g s
.SUBCKT NE34018/CEL 2 1 3
*B1  10  7 11 NE34000
Z1  10  7 11 NE34000
RDB 10 12 650
CBS 12 11 10E-12
Rsx 11  9 2
Rdx 10  8 1.5
Lg   4  7 0.93E-9
Ld   8  5 1.25E-9
Ls   9  6 0.25E-9
LGpkg  1  4 0.18
LDpkg  2  5 0.18
LSpkg  6  3 0.09
CGDpkg 4 5 0.02E-12
CDSpkg 5 6 0.1E-12
CGSpkg 4 6 0.1E-12
*.MODEL NE34000 GASFET
.MODEL NE34000 NMF 
*+(VTO=-0.6885   ALPHA=5          BETA=0.1838       GAMMA=0.038
+(VTO=-0.6885   ALPHA=5          BETA=0.1838       LAMBDA=0.038
+ Q=1.8         DELTA=0.25       VBI=0.7           IS=3e-13
+ N=1           TAU=4e-12        CDS=0.1e-12       CGS=0.95e-12
+ CGD=0.04e-12  VDELTA=0.05      FC=0.5            RG=4
+ RD=0          RS=0             KF=0              AF=1    
+ XTI=3         EG=1.43          VTOTC=0           BETATCE=0) 
.ENDS
*$

