* Library of Line Drivers, Receivers and Transceivers
*
* $Revision:   1.1  $
* $Author:   rperez  $
* $Date:   17 Nov 1998 10:48:52  $
*$
*******************************************************************
.SUBCKT 26C32 INV1 NONINV1 VOUT1 EN EN_BAR VCC GND
X1 INV1 NONINV1  ENABLE VOUT1 VCC GND C32_CKT
X10 EN EN_BAR ENABLE VCC GND C32_ENA
C1 ENABLE GND 1.2P
.ENDS
*$
*******************************************************************
.SUBCKT 26C32_4 INV1 NONINV1 VOUT1 EN EN_BAR VCC GND
+INV2 NONINV2 VOUT2 INV3 NONINV3 VOUT3 INV4 NONINV4 VOUT4
X1 INV1 NONINV1  ENABLE VOUT1 VCC GND C32_CKT
X2 INV2 NONINV2  ENABLE VOUT2 VCC GND C32_CKT
X3 INV3 NONINV3  ENABLE VOUT3 VCC GND C32_CKT
X4 INV4 NONINV4  ENABLE VOUT4 VCC GND C32_CKT
X10 EN EN_BAR ENABLE VCC GND C32_ENA
.ENDS
***************
.SUBCKT C32_CKT INV NONINV  ENABLE OUT VCC GND
RIN1 VCC NONINV 10K
RIN2 INV GND 10K
E101 101 GND TABLE {V(INV,NONINV)} = (-0.1, .5) (0.1, 4.5)
RI101 101 102 1K
CI101 102 GND 15P
RI102 102 103 9K
X1 103 2  VCC GND C32_INV
X2 2  22 VCC GND C32_INV
X2A 2 22 VCC GND C32_INV
R22 22 103 50K
X3 ENABLE 4 VCC GND C32_INV
M1 6 ENABLE GND GND  MNC32 L=4U W=2000U
M2 OUT  22  6   6    MNC32 L=4U W=2000U
M3 OUT  22  5   5    MPC32 L=4U W=4000U
M4 5     4  VCC VCC  MPC32 L=4U W=4000U
.MODEL MNC32 NMOS LEVEL=1 VTO=1  KP=30U CGDO=.1N CGSO=.1N CGBO=1N
.MODEL MPC32 PMOS LEVEL=1 VTO=-1 KP=15U CGDO=.1N CGSO=.1N CGBO=1N
.ENDS
**************
.SUBCKT C32_ENA EN EN_BAR ENA VCC GND
X1 EN 1 VCC GND C32_INV2
X2 1 2 VCC GND C32_INV
X3 EN_BAR 3 VCC GND C32_INV2
M1 5 2 GND GND MNC32 L=6U W=12U
M2 5 3 GND GND MNC32 L=6U W=12U
M3 5 2 4   4   MPC32 L=6U W=24U
M4 4 2 VCC VCC MPC32 L=6U W=24U
X4 5 ENA VCC GND C32_INV
X5 5 ENA VCC GND C32_INV
.MODEL MNC32 NMOS LEVEL=1 VTO=1  KP=30U CGDO=.1N CGSO=.1N CGBO=1N
.MODEL MPC32 PMOS LEVEL=1 VTO=-1 KP=15U CGDO=.1N CGSO=.1N CGBO=1N
.ENDS
**************
.SUBCKT C32_INV IN OUT VCC GND
M1 OUT IN GND GND MNC32 L=4U W=20U
M2 OUT IN VCC VCC MPC32 L=4U W=40U
.ENDS
**************
.SUBCKT C32_INV2 IN OUT VCC GND
M1 OUT IN GND GND MNC32 L=4U W=80U
M2 OUT IN VCC VCC MPC32 L=4U W=40U
.MODEL MNC32 NMOS LEVEL=1 VTO=1  KP=30U CGDO=.1N CGSO=.1N CGBO=1N
.MODEL MPC32 PMOS LEVEL=1 VTO=-1 KP=15U CGDO=.1N CGSO=.1N CGBO=1N
.ENDS
*$



