* [Disclaimer] (C) Copyright Texas Instruments Incorporated 1999 All rights reserved 
* Texas Instruments Incorporated hereby grants the user of this SPICE Macro-model a 
* non-exclusive, nontransferable license to use this SPICE Macro-model under the following 
* terms. Before using this SPICE Macro-model, the user should read this license. If the 
* user does not accept these terms, the SPICE Macro-model should be returned to Texas 
* Instruments within 30 days. The user is granted this license only to use the SPICE 
* Macro-model and is not granted rights to sell, load, rent, lease or license the SPICE 
* Macro-model in whole or in part, or in modified form to anyone other than user. User may 
* modify the SPICE Macro-model to suit its specific applications but rights to derivative 
* works and such modifications shall belong to Texas Instruments. This SPICE Macro-model is 
* provided on an "AS IS" basis and Texas Instruments makes absolutely no warranty with 
* respect to the information contained herein. TEXAS INSTRUMENTS DISCLAIMS AND CUSTOMER 
* WAIVES ALL WARRANTIES, EXPRESS OR IMPLIED, INCLUDING WARRANTIES OF MERCHANTABILITY OR 
* FITNESS FOR A PARTICULAR PURPOSE. The entire risk as to quality and performance is with 
* the Customer. ACCORDINGLY, IN NO EVENT SHALL THE COMPANY BE LIABLE FOR ANY DAMAGES, 
* WHETHER IN CONTRACT OR TORT,INCLUDING ANY LOST PROFITS OR OTHER INCIDENTAL, CONSEQUENTIAL,
* EXEMPLARY, OR PUNITIVE DAMAGES ARISING OUT OF THE USE OR APPLICATION OF THE SPICE 
* Macro-model provided in this package. Further, Texas Instruments reserves the right to 
* discontinue or make changes without notice to any product herein to improve reliability, 
* function, or design. Texas Instruments does not convey any license under patent rights or 
* any other intellectual property rights, including those of third parties.
*  
*  
* THS4131 SUBCIRCUIT
* FULLY DIFFERENTIAL HIGH SPEED MONLITHIC OPERATIONAL AMPLIFIER 
* WRITTEN 9/18/00
* TEMPLATE=X^@REFDES %IN+ %IN- %VCC+ %VCC- %OUT+ %OUT- %VOCM @MODEL
* CONNECTIONS:      NON-INVERTING INPUT
*                   | INVERTING INPUT
*                   | | POSITIVE POWER SUPPLY
*                   | | | NEGATIVE POWER SUPPLY
*                   | | | | OUTPUT +
*                   | | | | | OUTPUT -
*                   | | | | | |  VOCM
*                   | | | | | |  |
.SUBCKT THS4131     1 2 3 4 5 5b 17 
*
* INPUT *
 Q1          27 1  28 NPN_IN 2
 Q2          25 2  29 NPN_IN 2
 R2          26  29  15  
 R1          26  28  15  

* SECOND STAGE *
 Q3          07 Vref  27 PNP 2
 Q4          09 Vref  25 PNP 2
 Q5          07  105  06 NPN 1
 Q7          09  105  08 NPN 1
 R3           4  06  333  
 R4           4  08  333  
 Cc           0  09  25.5p  
 Ccb          0  07  25.5p  

* HIGH FREQUENCY SHAPING *
 Ehf          34 0  09 0 1
 Lhf          34  35  7n  
 Rhf          102  35  25  
 Chf         0  102  23p  
 Ehfb          24 0  07 0 1
 Lhfb          24  33  7n  
 Rhfb          12  33  25  
 Chfb         0  12  23p  

* OUTPUT *
 Q8          102  102  103 PNP 1
 Q9          102  102  104 NPN 1
 Q10         3  103  30 NPN 5
 Q11         4  104  31 PNP 7.5
 R5          5 30  4  
 R7          31  5  4  
 Q8b          12  12  13 PNP 1
 Q9b          12  12  15 NPN 1
 Q10b         3  13  22 NPN 5
 Q11b         4  15  23 PNP 7.5
 R5b          5b  22  4  
 R7b          23  5b  4  

* Vcm ERROR AMP *
  Gcm          0 105  16  17b 1e-4
  Vcm		   17 17b 285e-3
  Rtop         17 3 30k
  Rbot         17 4 30k   
  Rcm          16 5  10k  
  Ccm          16 5b  100p  
  Rcm2         16 5b  10k  
  Ccm2         16 5  100p  

* BIAS SOURCES *
 V1         3 Vref 1.85
 I1         3 27 DC 2.1e-3  
 I2         3 25 DC 2.1e-3  
 I3         0 103 DC 1.225e-3  
 I4         26 4 DC 2.1e-3  
 I5         104 0 DC 1.86e-3  
 I6         0 13 DC 1.225e-3  
 I7         15 0 DC 1.86e-3  
 
.MODEL NPN_IN NPN
+ IS=170E-18 BF=400 NF=1 VAF=100 IKF=0.0389 ISE=7.6E-18
+ NE=1.13489 BR=1.11868 NR=1 VAR=4.46837 IKR=8 ISC=8E-15
+ NC=1.8 RB=25 RE=0.1220 RC=20 CJE=120.2E-15 VJE=1.0888 MJE=0.381406
+ VJC=0.589703 MJC=0.265838 FC=0.1 CJC=133.8E-15 XTF=272.204 TF=12.13E-12
+ VTF=10 ITF=0.147 TR=3E-09 XTB=1 XTI=5 KF=7.5E-14

.MODEL NPN NPN
+ IS=170E-18 BF=100 NF=1 VAF=100 IKF=0.0389 ISE=7.6E-18
+ NE=1.13489 BR=1.11868 NR=1 VAR=4.46837 IKR=8 ISC=8E-15
+ NC=1.8 RB=250 RE=0.1220 RC=200 CJE=120.2E-15 VJE=1.0888 MJE=0.381406
+ VJC=0.589703 MJC=0.265838 FC=0.1 CJC=133.8E-15 XTF=272.204 TF=12.13E-12
+ VTF=10 ITF=0.147 TR=3E-09 XTB=1 XTI=5

.MODEL PNP PNP
+ IS=296E-18 BF=100 NF=1 VAF=100 IKF=0.021 ISE=494E-18
+ NE=1.49168 BR=0.491925 NR=1 VAR=2.35634 IKR=8 ISC=8E-15
+ NC=1.8 RB=250 RE=0.1220 RC=200 CJE=120.2E-15 VJE=0.940007 MJE=0.55
+ VJC=0.588526 MJC=0.55 FC=0.1 CJC=133.8E-15 XTF=141.135 TF=12.13E-12
+ VTF=6.82756 ITF=0.267 TR=3E-09 XTB=1 XTI=5

.ENDS
