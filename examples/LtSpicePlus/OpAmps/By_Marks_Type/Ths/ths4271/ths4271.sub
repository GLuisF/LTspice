* [Disclaimer] (C) Copyright Texas Instruments Incorporated 1999 All rights reserved
* Texas Instruments Incorporated hereby grants the user of this SPICE Macro-model a
* non-exclusive, nontransferable license to use this SPICE Macro-model under the following
* terms. Before using this SPICE Macro-model, the user should read this license. If the
* user does not accept these terms, the SPICE Macro-model should be returned to Texas
* Instruments within 30 days. The user is granted this license only to use the SPICE
* Macro-model and is not granted rights to sell, load, rent, lease or license the SPICE
* Macro-model in whole or in part, or in modified form to anyone other than user. User may
* modify the SPICE Macro-model to suit its specific applications but rights to derivative
* works and such modifications shall belong to Texas Instruments. This SPICE Macro-model is
* provided on an "AS IS" basis and Texas Instruments makes absolutely no warranty with
* respect to the information contained herein. TEXAS INSTRUMENTS DISCLAIMS AND CUSTOMER
* WAIVES ALL WARRANTIES, EXPRESS OR IMPLIED, INCLUDING WARRANTIES OF MERCHANTABILITY OR
* FITNESS FOR A PARTICULAR PURPOSE. The entire risk as to quality and performance is with
* the Customer. ACCORDINGLY, IN NO EVENT SHALL THE COMPANY BE LIABLE FOR ANY DAMAGES,
* WHETHER IN CONTRACT OR TORT,INCLUDING ANY LOST PROFITS OR OTHER INCIDENTAL, CONSEQUENTIAL,
* EXEMPLARY, OR PUNITIVE DAMAGES ARISING OUT OF THE USE OR APPLICATION OF THE SPICE
* Macro-model provided in this package. Further, Texas Instruments reserves the right to
* discontinue or make changes without notice to any product herein to improve reliability,
* function, or design. Texas Instruments does not convey any license under patent rights or
* any other intellectual property rights, including those of third parties.

* THS4271 High Speed amplifier "macromodel" subcircuit
* updated using Model Editor release 9.2 on 11/05/02 at 13:11
* The Model Editor is a PSpice product.
*
******Modified 2008-08-12 by Tom Bruhns for use with LTSpice
******Modifications include pin assignments to follow pin
******numbering on an SO-8 (or other 8 pin) package, so that
******a generated project netlist can be more gracefully
******imported into the FreePCB package for layout.
******This includes adding "dummy" resistors connected to
******the SO-8 "no connect" pins, 1, 5 and 8.  You should
******connect the NC pins to something benign in your circuit.
*
* connections:   pin 1: no connection
*                |   inverting input
*                |   |   non-inverting input
*                |   |   |   negative power supply
*                |   |   |   |  pin 5: no connection
*                |   |   |   |  |   output
*                |   |   |   |  |   |   positive power supply
*                |   |   |   |  |   |   |  pin 8: no connection
*                |   |   |   |  |   |   |  |
*                |   |   |   |  |   |   |  |
*$
.subckt THS4271  nc1 inm inp V- nc5 out V+ nc8

* No connect pins:  to keep Spice happy, they need to connect to something benign:
rnc1	nc1 V+ 1e9
rnc5	nc5 V+ 1e9
rnc8	nc8 V+ 1e9

*Offset and CMRR
  Vos  1a  9  .005
  Ios  inp  inm  .5u

* upper Vic range limit
  drc1 16 17 dx
  drc2 16 18 dx
  Vcp   V+ 16 dc -0.4

* input stage
  rc1  17 11 176.8
  rc2  18 12 176.8
  L-    inm 2a .8n
  q1   11 2a 13 qx1
  L+    inp 1a .8n
  q2   12  9 14 qx1
  re1  13 10 159.07
  re2  14 10 159.07
  Cdif  inp  1c  0.8p
  Rcdf  1c inm   50
  Ccm   inm  2b  0.4p
  Rccm  2b 99  50

* lower Vic range limit
  d10  15 10 dx
  v10  15  V- dc 1.2
  Iee  10  V- dc 3e-3
  Icc   V+  V-  15m
  Rcc   V+  V-  2500

* gain stage and dominant pole
  ga   21 99 value = {(limit(V(11,12),-.447,.447))*-35.6m}
  ra   21 99 158k
  ca   21 99 15.9E-12

* GAIN STAGE SWING LIMIT
  DPC  21 23 dx
  VPC   V+ 23 1.7
  DNC  24 21 dx
  VNC  24 V-  1.74

* zero
  ez   26  99  21  99 10
  rz1  26  27  900
  cz   26  27  .2p
  rz2  27  99  100

* phase shift stage
  gps   25 99 27 99 -100.0E-6
  rps   25 99 10.0E3
  cps   25 99 10E-15

  egnd  99  0  poly(2)  (V+,0) (V-,0)  0 .5  .5
  X_OP  25 99  V+ V- 5a THS4271_OP
  Ro    5a 5b  .1
  Lo    5b out  .2n
  Rco   5c 99  10
  Co    out  5c .8p

.ends
*$

* Output stage
* connections:      non-inverting input
*                   | inverting input
*                   | | positive power supply
*                   | | | negative power supply
*                   | | | | output
*                   | | | | |
.subckt THS4271_OP  1 2 3 4 5
* GAIN STAGE SWING LIMIT
  DOPC   1 38  dx
  VOPC   3 38  1.
  DONC  48  1  dx
  VONC  48  4  1.

* UPPER DRIVE STAGE
  ROP   3  34  8.5
  HLP2 34  33  VLP 30
  VOP  33  32  0
  HLP1 35   0  VOP  8
  RLP  35  36  8
  DLP  36  37  dx
  VLP  37   0  0
  EOP  32  31  Poly(2)   (2,1) (3,4)  -.8  1  .5
  DOP  31   5  dx

* LOWER DRIVE STAGE
  DON   5  41  dx
  EON  41  42  Poly(2)   (1,2) (3,4)  -.8  1  .5
  VON  42  43  0
  HLN1 45   0  VON  15
  RLN  45  46  8
  DLN  46  47  dx
  VLN  47   0  0
  HLN2 43  44  VLN 45
  RON  44   4  12

.ends
*$
*DIODE MODELS
.model dx D(Is=800.00E-18)
*$
.model qx1 NPN(Is=800.00E-18 Bf=272.73 af=0 kf=9e-22)
*.model qx1 NPN(Is=800.00E-18 Bf=272.73 af=2 kf=8e-8)
*$
