**************************************
*      Model Generated by MODPEX     *
*Copyright(c) Symmetry Design Systems*
*         All Rights Reserved        *
*    UNPUBLISHED LICENSED SOFTWARE   *
*   Contains Proprietary Information *
*      Which is The Property of      *
*     SYMMETRY OR ITS LICENSORS      *
*    Modeling services provided by   *
* Interface Technologies www.i-t.com *
**************************************
*ON-Semiconductors
.MODEL QBD241C npn
+IS=1e-09 BF=144.987 NF=0.521792 VAF=16.0104
+IKF=1.39046 ISE=8.61786e-12 NE=3.32675 BR=0.1
+NR=0.75 VAR=19.8928 IKR=10 ISC=3.98816e-12
+NC=3.60784 RB=0.1 IRB=0.1 RBM=0.1
+RE=0.0001 RC=0.0646733 XTB=0.864855 XTI=1
+EG=1.05 CJE=3.18622e-10 VJE=0.491278 MJE=0.48194
+TF=1e-08 XTF=4.00977 VTF=7.58156 ITF=0.001
+CJC=3.12657e-10 VJC=0.63057 MJC=0.450516 XCJC=0.799994
+FC=0.511498 CJS=0 VJS=0.75 MJS=0.5
+TR=9.57121e-06 PTF=0 KF=0 AF=1
* Model generated on Feb 22, 2004
* Model format: PSpice


******************************************************************************************
******************************************************************************************


**************************************
*      Model Generated by MODPEX     *
*Copyright(c) Symmetry Design Systems*
*         All Rights Reserved        *
*    UNPUBLISHED LICENSED SOFTWARE   *
*   Contains Proprietary Information *
*      Which is The Property of      *
*     SYMMETRY OR ITS LICENSORS      *
*    Modeling services provided by   *
* Interface Technologies www.i-t.com *
**************************************
*ON-Semiconductors
.MODEL Q2N3055 npn
+IS=2.37426e-14 BF=129.119 NF=0.85 VAF=31.1252
+IKF=0.990922 ISE=2.47498e-10 NE=1.89002 BR=1.01252
+NR=0.924456 VAR=254.624 IKR=2.70227 ISC=2.47498e-10
+NC=2.90624 RB=3.66609 IRB=0.1 RBM=0.1
+RE=0.000352673 RC=0.0764459 XTB=1.34801 XTI=1.07207
+EG=1.206 CJE=9.03089e-08 VJE=0.513954 MJE=0.59999
+TF=1e-08 XTF=1.36696 VTF=1.02605 ITF=0.987296
+CJC=5e-10 VJC=0.400243 MJC=0.410238 XCJC=0.803124
+FC=0.661216 CJS=0 VJS=0.75 MJS=0.5
+TR=1e-07 PTF=0 KF=0 AF=1
* Model generated on Jan 24, 2004
* Model format: PSpice


******************************************************************************************
******************************************************************************************


* From National Linear.lib
*---------------------------------------------------------------LM723
* connections:
*              NC
*              | current limit
*              | | current sense
*              | | | inverting input
*              | | | | non-inverting input
*              | | | | | Vref
*              | | | | | | Vcc-
*              | | | | | | | NC 
*              | | | | | | | | Vz 
*              | | | | | | | | | Vout  
*              | | | | | | | | | |  Vc    
*              | | | | | | | | | |  |   Vcc+         
*              | | | | | | | | | |  |  |  freq. compensation
*              | | | | | | | | | |  |  |  |  NC
.SUBCKT LM723    2 3 4 5 6 7   9 10 11 12 13 
*
* Note: This model is based on the National LM723 voltage
*       regulator.  All characterization is from data sheet
*       information.  The pin configuration corresponds to
*       the dual-in-line package.  Therefore, it includes
*       an internal 6.2 volt zener diode between Vout and Vz
*       In the model, GIee & GIcc adjust the short circuit
*       current limit and the standby current.  Rsb and the
*       temperature coefficient on RIee also affect the
*       standby current.  Bf and the transresistance term
*       on HVref adjust the low frequency output impedence
*       and the load regulation.  Rlnreg controls the line
*       regulation and ripple rejection.  Rref and its
*       temperature coefficient determine the average
*       temperature coefficient with respect to the output
*       voltage.
*
* Standby Current Correction
*
Rsb 12 7 300k
*
* Error Amplifier
*
Rlnreg 12 13 4meg
*Icc 12 13 DC 583ua
*Iee 20  7 DC 1166ua
Iee   0 24 1166ua
RIee 24 0 1 TC=4E-3
GIee (20,7) (24,0) 1.0
GIcc (12,13) (24,0) 0.5
Q5 12 5 20 Npn1
Q4 13 4 20 Npn1
*
* Voltage Reference
*
HVref 22 7 POLY(1) Vmon 7.15 0.0
Rref 22 6 15ohm TC=0.01
*
* Output Stage
*
Q1 12 13 21 Npn1
Q2 11 21 23 Npn1
Vmon 23 10 DC 0.0
Re 21 10 15k
.MODEL Npn1 NPN (Bf=55 Is=1E-14)
*
* Frequency Compensation, Current Limit, Current Sense
*
Q3 13 2 3 Npn1
R2 2  7 1.0e12
R3 3  7 1.0e12
*
* Zener Diode (6.2V) to pin 9
*
Dz 9 10 Dz
Rz 9  7 1.0e12
.MODEL Dz D (Is=0.05p Rs=4 Bv=5.79 Ibv=0.05u)
*
.ENDS


******************************************************************************************
******************************************************************************************


*//////////////////////////////////////////////////////////////////////
* (C) National Semiconductor, Inc.
* Models developed and under copyright by:
* National Semiconductor, Inc.  

*/////////////////////////////////////////////////////////////////////
* Legal Notice: This material is intended for free software support.
* The file may be copied, and distributed; however, reselling the 
*  material is illegal

*////////////////////////////////////////////////////////////////////
* For ordering or technical information on these models, contact:
* National Semiconductor's Customer Response Center
*                 7:00 A.M.--7:00 P.M.  U.S. Central Time
*                                (800) 272-9959
* For Applications support, contact the Internet address:
*  amps-apps@galaxy.nsc.com

*//////////////////////////////////////////////////////////
*LM741 OPERATIONAL AMPLIFIER MACRO-MODEL
*//////////////////////////////////////////////////////////
*
* connections:      non-inverting input
*                   |   inverting input
*                   |   |   positive power supply
*                   |   |   |   negative power supply
*                   |   |   |   |   output
*                   |   |   |   |   |
*                   |   |   |   |   |
.SUBCKT LM741       1   2  99  50  28
*
*Features:
*Improved performance over industry standards
*Plug-in replacement for LM709,LM201,MC1439,748
*Input and output overload protection
*
****************INPUT STAGE**************
*
IOS 2 1 20N
*^Input offset current
R1 1 3 250K
R2 3 2 250K
I1 4 50 100U
R3 5 99 517
R4 6 99 517
Q1 5 2 4 QX
Q2 6 7 4 QX
*Fp2=2.55 MHz
C4 5 6 60.3614P
*
***********COMMON MODE EFFECT***********
*
I2 99 50 1.6MA
*^Quiescent supply current
EOS 7 1 POLY(1) 16 49 1E-3 1
*Input offset voltage.^
R8 99 49 40K
R9 49 50 40K
*
*********OUTPUT VOLTAGE LIMITING********
V2 99 8 1.63
D1 9 8 DX
D2 10 9 DX
V3 10 50 1.63
*
**************SECOND STAGE**************
*
EH 99 98 99 49 1
G1 98 9 5 6 2.1E-3
*Fp1=5 Hz
R5 98 9 95.493MEG
C3 98 9 333.33P
*
***************POLE STAGE***************
*
*Fp=30 MHz
G3 98 15 9 49 1E-6
R12 98 15 1MEG
C5 98 15 5.3052E-15
*
*********COMMON-MODE ZERO STAGE*********
*
*Fpcm=300 Hz
G4 98 16 3 49 3.1623E-8
L2 98 17 530.5M
R13 17 16 1K
*
**************OUTPUT STAGE**************
*
F6 50 99 POLY(1) V6 450U 1
E1 99 23 99 15 1
R16 24 23 25
D5 26 24 DX
V6 26 22 0.65V
R17 23 25 25
D6 25 27 DX
V7 22 27 0.65V
V5 22 21 0.18V
D4 21 15 DX
V4 20 22 0.18V
D3 15 20 DX
L3 22 28 100P
RL3 22 28 100K
*
***************MODELS USED**************
*
.MODEL DX D(IS=1E-15)
.MODEL QX NPN(BF=625)
*
.ENDS
*$


******************************************************************************************
******************************************************************************************


* UA741 OPERATIONAL AMPLIFIER "MACROMODEL" SUBCIRCUIT
* CREATED USING PARTS RELEASE 4.01 ON 07/05/89 AT 09:09
* (REV N/A)      SUPPLY VOLTAGE: +/-15V
* CONNECTIONS:   NON-INVERTING INPUT
*                | INVERTING INPUT
*                | | POSITIVE POWER SUPPLY
*                | | | NEGATIVE POWER SUPPLY
*                | | | | OUTPUT
*                | | | | |
.SUBCKT UA741    1 2 3 4 5
*
  C1   11 12 4.664E-12
  C2    6  7 20.00E-12
  DC    5 53 DX
  DE   54  5 DX
  DLP  90 91 DX
  DLN  92 90 DX
  DP    4  3 DX
  EGND 99  0 POLY(2) (3,0) (4,0) 0 .5 .5
  FB    7 99 POLY(5) VB VC VE VLP VLN 0 10.61E6 -10E6 10E6 10E6 -10E6
  GA 6  0 11 12 137.7E-6
  GCM 0  6 10 99 2.574E-9
  IEE  10  4 DC 10.16E-6
  HLIM 90  0 VLIM 1K
  Q1   11  2 13 QX
  Q2   12  1 14 QX
  R2    6  9 100.0E3
  RC1   3 11 7.957E3
  RC2   3 12 7.957E3
  RE1  13 10 2.740E3
  RE2  14 10 2.740E3
  REE  10 99 19.69E6
  RO1   8  5 150
  RO2   7 99 150
  RP    3  4 18.11E3
  VB    9  0 DC 0
  VC 3 53 DC 2.600
  VE   54  4 DC 2.600
  VLIM  7  8 DC 0
  VLP  91  0 DC 25
  VLN   0 92 DC 25
.MODEL DX D(IS=800.0E-18)
.MODEL QX NPN(IS=800.0E-18 BF=62.50)
.ENDS


******************************************************************************************
******************************************************************************************


.model BC559C   PNP(Is=1.02f Xti=3 Eg=1.11 Vaf=34.62 Bf=401.6 Ise=38.26p
+               Ne=5.635 Ikf=74.73m Nk=.512 Xtb=1.5 Br=9.011 Isc=1.517f Vtf=10
+               Nc=1.831 Ikr=.1469 Rc=1.151 Cjc=9.81p Mjc=.332 Vjc=.4865 Fc=.5
+               Cje=30p Mje=.3333 Vje=.5 Tr=10n Tf=524p Itf=.9847 Xtf=17.71)
*		PHILIPS		pid=bc559c	case=TO92		91-08-02 dsq


******************************************************************************************
******************************************************************************************


**************************************
*      Model Generated by MODPEX     *
*Copyright(c) Symmetry Design Systems*
*         All Rights Reserved        *
*    UNPUBLISHED LICENSED SOFTWARE   *
*   Contains Proprietary Information *
*      Which is The Property of      *
*     SYMMETRY OR ITS LICENSORS      *
*    Modeling services provided by   *
* Interface Technologies www.i-t.com *
**************************************
*ON-Semiconductors
.MODEL D1N4001 D
+IS=1.22478e-08 RS=0.0414786 N=1.83369 EG=0.6
+XTI=0.05 BV=50 IBV=5e-08 CJO=1e-11
+VJ=0.7 M=0.5 FC=0.5 TT=1e-09
+KF=0 AF=1
* Model generated on October 12, 2003
* Model format: PSpice


******************************************************************************************
******************************************************************************************


*1N4001 General Purpose Diode 50V  1 A  4us  Si Diode pkg:DO-41 
  * .MODEL D1N4001 D(IS=2.55E-9 RS=0.042 N=1.75 TT=5.76E-6 CJO=1.85E-11 
  * + VJ=0.75 M=0.333 BV=50 IBV=1E-5 )


******************************************************************************************
******************************************************************************************


*1N4148 Fast Si Diode TELEFUNKEN	pid=d1n4148	case=DO35
.model D1N4148  D(Is=2.682n N=1.836 Rs=.5664 Ikf=44.17m Xti=3 Eg=1.11 Cjo=4p
+ M=.3333 Vj=.5 Fc=.5 Isr=1.565n Nr=2 Bv=100 Ibv=100u Tt=11.54n)


******************************************************************************************
******************************************************************************************


* BZX85C3V3 - 33 Series 1.3Watt Zeners - FAIRCHILD
* ------------------------------------------------
.MODEL  ZD3V3  D
+ IS	1.620E-14
+ RS	4.929E-2
+ N	1.111
+ XTI	3.0
+ EG	1.110
+ CJO	2.020E-10
+ M	6.854E-2
+ VJ	0.3907
+ FC	0.5
+ ISR	9.900E-4
+ NR	4.990
+ BV	3.409
+ IBV	6.099E-2


******************************************************************************************
******************************************************************************************


* BZX84C15V D model
* created using Parts release 7.1 on 03/31/98 at 08:52
* Parts is a MicroSim product.

*.MODEL ZD15 D
*+ IS=2.6665E-18
*+ N=.82284
*+ RS=.51617
*+ IKF=11.760E-3
*+ CJO=28.508E-12
*+ M=.33087
*+ VJ=.64691
*+ ISR=38.989E-12
*+ BV=15.119
*+ IBV=.54544
*+ TT=2.9720E-6
**$


******************************************************************************************
******************************************************************************************


*SRC=1N4744A;DI_1N4744A;Diodes;Zener 10V-50V; 15.0V  1.00W   Diodes Inc.
*Zener
*SYM=HZEN
.SUBCKT ZD15  1 2
*        Terminals    A   K
D1 1 2 DF
DZ 3 1 DR
VZ 2 3 13.7
.MODEL DF D ( IS=27.5p RS=0.620 N=1.10
+ CJO=78.3p VJ=1.00 M=0.330 TT=50.1n )
.MODEL DR D ( IS=5.49f RS=0.804 N=1.77 )
.ENDS


******************************************************************************************
******************************************************************************************


*Original is QTLP690C
.model LED D(Is=1e-22 Rs=6 N=1.5 Cjo=50p Iave=160m Vpk=5 Bv=5 mfg=Fairchild type=LED)


******************************************************************************************
******************************************************************************************


*--------------------- The Linear Potentiometer ---------------------
*      
* 1.0 <----- 0.0
*        |3
*      __V__
*  1--|_____|--2
*        
*  o--R1-o-R2--o
*
.SUBCKT potentiometer 1 2 3
* Parameters: Rtot, wiper
.param w=limit(0.01m,wiper,0.99999)
*
R1 1 3 {Rtot*(1-w)}
R2 3 2 {Rtot*(w)}
.ENDS
*

.SUBCKT pot_lin 1 2 3
.param w=limit(0.01m,wiper,0.99999)
*
R1 1 3 {Rtot*(1-w)}
R2 3 2 {Rtot*(w)}
.ENDS


******************************************************************************************
******************************************************************************************
