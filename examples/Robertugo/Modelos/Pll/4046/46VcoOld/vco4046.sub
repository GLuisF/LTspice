* 4046 VCO Section:
* Rin= VCO Input Resistace S1= Voltage Limiter linear slope. S2= Voltage Limiter non-linear slope.
* Kb = Arbitrary constant to adjust the value of the conversion gain (transimpedance gain).
* Vfree= Frequency dependent constant in Emult. Kc= Negative inverse amplitude of the square wave.
* M1 & M2 = Current mirror multiplier to adjust oscillator frequency.
.Subckt 4046Vco Vcoin r1 r2 ce1 ce2 vcout inh zener vdd vss
Rin Vcoin Vss {Rin}
Rvdd Vdd Vss 1g
Blim Vlim Vss V=(if(v(vcoin,vss)<v(vdd,vss),S1*v(vcoin,vss),S2*(v(vcoin,vss)-v(vdd,vss))+v(vdd,vss)))
Rlim Vlim Vss 1e6
Bmul Mix Vss V=(v(vlim)*Kb+Vfree)
Rmul Mix Vss 1
Er2 Ir2 Vss Vdd Ir2 1e3
Vr2 Ir2 r2 0
Er1 Ir1 Vss Mix Ir1 1e3
Vr1 Ir1 r1 0
Bosc Adj Vss V=TABLE ((abs((V(vdd)/I(VR2))/(V(mix)/I(VR1)))),.5,1.43, 1,1.6, 10,1.04, 50,.67, 100,.84, 101,1, 102,1, 1000,1)
Radj Adj Vss 1g
Bim Ce1 Vss I=((M1*I(VR1)*V(adj)+M2*I(VR2))*Kc*V(sqr))
Vcext Ce2 Vss 0
Cstray Ce1 Ce2 6p
Rcext Ce1 Ce2 1g
Bsqr Pul Vss V=(-1e7*V(Ce1)+1.2e6*V(sqr))
Rsq Sqr Pul .1T
Csq Sqr Vss 10f
Dsq1 Sqr 13 Dio
Vsq1 13 Vss {Vdd}
Dsq2 14 Sqr Dio
Vsq2 14 Vss {-Vdd}
BOut Vcout Vss V=table ((v(vdd)*v(off)*(v(sqr)/Vdd)),.1,.1, v(vdd),v(vdd))
Rinh Inh Vss 1e6
Boff Off Vss V=(if(v(inh)<0.9,1.0,0.0))
Roff Off Vss 1e6
Dzener Vss Zener Zen
Rz Zener Vss 1g
*Bdem Demout Vss V=table ((2e8*v(vcoin,demout)*v(off)),-20,-20, 20,20)
*Rdemout Demout Vss 1g
.model zen D(Is=1.004f Rs=.5875 Ikf=0 N=1 Xti=3
+Eg=1.11 Cjo=160pM=.5484 Vj=.75 Fc=.5 Isr=1.8n
+Nr=2 Bv=5.2 Ibv=27.721mNbv=1.1779 Ibvl=1.1646m
+Nbvl=21.894 Tbv1=176.47u)
.model Dio D (IS=10u N=0.1 CJO=80f RS=1m)
.ends