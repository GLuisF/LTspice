.SUBCKT VPVCO     1     2       3        4  5  6
*              INPUT OUTPUT XDOT/OMEGA  KS  A  fo
* VOLTAGE-PROGRAMMABLE VCO MODEL
*  VOLTAGE ANALOG PARAMETERS:
*    KS IS FREQUENCY SENSITIVITY IN KILOHERTZ/VOLT
*     A IS AMPLITUDE OF OSCILLATION IN VOLTS
*    fo IS OSCILLATION FREQUENCY IN KILOHERTZ WHEN INPUT IS ZERO
RIN 1 0 1G
R45 4 5 1G
R6  6 0 1G
**IMPLEMENT VOLTAGE CONTROLLED FREQUENCY TERMS FOR INTEGRATOR #1
 G1A 0 3 POLY(3) 7 0 1 0 4 0  0 0 0 0 0 0 0 0 0 0 0 0 0 0 1
 G1B 0 3 POLY(2) 7 0 6 0  0 0 0 0 1
 C1 3 10 .159154943M
 R1 3 0 1G
**IMPLEMENT VOLTAGE CONTROLLED FREQUENCY TERMS FOR INTEGRATOR #2
 G2A 0 8 POLY(3)  3 0 1 0 4 0  0 0 0 0 0 0 0 0 0 0 0 0 0 0 1
 G2B 0 8 POLY(2)  3 0 6 0  0 0 0 0 1
 C2 8 0 .159154943M
 R2 8 0 1G
**IMPLEMENT FIRST INTEGRAND WITH VARIABLE DAMPING TERM
 EXDD 7 0 POLY(3) 3 0 8 0 5 0
+ 0 0 -1 0 0 0 0 0 0 0 -.1 0 0 -.1 0  .1
* - -  P2   - - - - - -  P10 - - P13 - P15
 R7 7 0 1G
**OUTPUT BUFFER STAGE
 REO 2 0 1G
 EO 2 0 8 0 1
**START-UP CIRCUITRY
 VSTART 9 0 PULSE (1 0)
 R9 9 0 1G
* SET INITIAL AMPLITUDE TO V(5) VOLTAGE
  EIC 10 0 POLY(2) 9 0 5 0  0 0 0 0 -1
.ENDS VPVCO