* Copyright © Linear Technology Corp. 1998, 1999, 2000, 2001, 2002, 2003, 2004, 2005.  All rights reserved.
*
.subckt 2pole 1 2 3 4 5
S1 5 3 N001 5 Q
S2 4 5 5 N001 Q
A1 2 1 0 0 0 0 N002 0 OTA G={Avol/Rout} ref={Vos} Iout={slew*Cout} Cout={Cout} en={en} enk={enk} in={in} ink={ink} Vhigh=1e308 Vlow=-1e308
C3 5 4 1p
C4 3 5 1p
R3 3 2 {2*Rin} noiseless
G1 0 N001 N002 0 {1/Rout}
R2 N001 0 {Rout} noiseless
C1 N001 0 {X*Cout/Avol}
R4 3 1 {2*Rin} noiseless
R5 1 4 {2*Rin} noiseless
R6 2 4 {2*Rin} noiseless
G2 0 N002 4 N002 table(0 0 10 {2*slew*Cout})
G3 N002 0 N002 3 table(0 0 10 {2*slew*Cout})
R9 3 N002 {2*Rout} noiseless
R10 N002 4 {2*Rout} noiseless
.param Rout=100Meg
.param Cout={Avol/GBW/2/pi/Rout}
.model Q SW(Ron=10 Roff=10Meg Vt=0 Vh=-.1 Vser={Rail} ilimit={Ilimit} noiseless)
.param Avol=1Meg GBW=10Meg Slew=10Meg rail=0 Vos=0 ilimit=25m
.param en=0 enk=0 in=0 ink=0 phimargin=45 Rin=500Meg
.param X table(phimargin,29.4,3.5,32.1,2.9,33.8,2.6,35.8,2.3,38.1,2,40.9,1.7,43.2,1.5,45.9,1.3,49.2,1.1,53.2,0.9,58.2,0.7,64.7,0.5,73,0.3,86.1,0.05)
.ends 2pole
