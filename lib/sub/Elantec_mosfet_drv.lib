*EL7243C Macromodel
* Revision A, January 1996
*Connection    Gnd
*               |       Inp+
*               |       |       Inp-
*               |       |       |       out
*               |       |       |       |       Vcc
.subckt EL7243C 1       2       3       8       10 
V1 12 1 1.6
R1 13 15 1k 
R2 14 15 5k  
R5 11 12 100 
C1 15 1 43.3pF
D1 14 13 dmod
X1 13 11 2 1 comp1
X2 16 12 15 1 comp1
V2 22 1 1.6
R6 23 25 1K
R7 24 25 5K
R8 21 22 100
C2 25 1 43.3pF
D2 24 23 dmod
X3 23 21 3 1 comp1
X4 26 25 22 1 comp1
X5 16 26 17 1 And-gate
sp 10 8 17 1 spmod
sn 8 1 17 1 snmod 
g1 11 1 13 1 938u
g2 21 1 23 1 938u
.model dmod d
.model spmod vswitch ron=3 roff=2meg von=1 voff=1.5
.model snmod vswitch ron=4 roff=2meg von=3 voff=2
* AND Gate Subcircuit*
.subckt And-gate inp1 inp2 out-A Vss-A
e1 out-A Vss-A table {v(inp1)*v(inp2)} = (0,3.2) (3.2,0)
Rout-a out-a vss-a 10meg
rinpa inp1 vss-a 10meg
rinpb inp2 vss-a 10meg
.ends and-gate
* Comparator Subcircuit *
.subckt comp1 out inp inm vss
e1 out vss table {(v(inp)-v(inm))*5000} = (0,0) (3.2,3.2)
Rout out vss 10meg
Rinp inp vss 10meg
Rinm inm vss 10meg
.ends comp1
.ends EL7243C
*
*EL7155C Spice Model
*Revision A, March 2002
*Connection	Vs+
*		|	OE
*		|	|	VIN
*		|	|	|	GND
*		|	|	|	|	VL
*		|	|	|	|	|	OUTL
*		|	|	|	|	|	|	OUTH
*		|	|	|	|	|	|	|	VH
*		|	|	|	|	|	|	|	|
.subckt EL7155C	1	2	3	4	5	6	7	8
c1 3 4 3.5pF
r1 3 4 10Meg
sp1 11 1 3 4 swp
rp1 11 4 10K
*
* OE pin input
c2 2 4 3.5p 
r2 2 4 10Meg
*
*Voutp path
e1 10 4 11 4 1
sp3 10 13 2 4 swp
r5 10 4 1k
r3 13 1 10K
sp9 7 8 4 13 swp1
r7 13 4 10k
d1 5 7 den
d3 7 1 den
clh 7 4 50pF
*
*VoutL path
e2 9 4 11 4 1
sp4 9 14 2 4 swp
r4 14 4 10K
r6 9 4 1k
sp10 6 5 14 4 swn
d2 5 6 den
d4 6 1 den
Cll 6 4 50pF
*Set the lowest VL --- -5V lower than gnd
V1 4 15 5
d5 15 5 den
*
* switch model
.model swp vswitch ron=4 roff=10Meg von=2.4 voff=2
.model swn vswitch ron=4 roff=10Meg von=5 voff =4
.model swp1 vswitch ron=4 roff=10Meg von=0 voff=-1
.model den D (n=1)
.ends EL7155C
*$
*EL7156C Spice Model
*Revision A, March 2002
*Connection	Vs+
*		|	OE
*		|	|	VIN
*		|	|	|	GND
*		|	|	|	|	VS-
*		|	|	|	|	|	VL
*		|	|	|	|	|	|	OUT
*		|	|	|	|	|	|	|	VH
*		|	|	|	|	|	|	|	|
.subckt EL7156C	1	2	3	4	5	6	7	8
c1 3 4 3.5pF
r1 3 4 10Meg
sp1 11 1 3 4 swp
rp1 11 4 10K
*
* OE pin input
c2 2 4 3.5p 
r2 2 4 10Meg
*
*Voutp path
e1 10 4 11 4 1
sp3 10 13 2 4 swp
r5 10 4 1k
r3 13 1 10K
sp9 7 8 4 13 swp1
r7 13 4 10k
d1 5 7 den
d3 7 1 den
clh 7 4 100pF
*
*VoutL path
e2 9 4 11 4 1
sp4 9 14 2 4 swp
r4 14 4 10K
r6 9 4 1k
sp10 6 7 14 4 swn
d2 5 6 den
*
* switch model
.model swp vswitch ron=4 roff=10Meg von=2.4 voff=2
.model swn vswitch ron=4 roff=10Meg von=5 voff =4
.model swp1 vswitch ron=4 roff=10Meg von=0 voff=-1
.model den D (n=1)
.ends EL7156C
*$

*EL7182C Macromodel
*Revision B, August 1997
*Connection     	In
*               	|       Gnd
*               	|       |       Vcc
*               	|       |       |       Out
*			|	  |	    |	      |    /Out
.subckt EL7182C   2       3       6       5	7 
V1 12 3 1.6
R1 13 15 1k 
R2 14 15 5k  
R5 11 12 100 
C1 15 3 33.3pF
D1 14 13 dmod
X1 13 11 2 3 comp1
X2 16 12 15 3 comp1
sp 6 7 16 3 spmod
sn 7 3 16 3 snmod 
g1 11 0 15 0 -940u
V11 121 3 1.6
R11 131 151 1k 
R21 141 151 5k  
R51 111 121 100 
C11 151 3 33.3pF
D11 141 131 dmod
X3 131 111 2 3 comp1
X4 161 151 121 3 comp1
sp1 6 5 161 3 spmod
sn1 5 3 161 3 snmod 
g11 11 0 15 0 -940u
.model dmod d
.model spmod vswitch ron=3 roff=2meg von=1 voff=1.5
.model snmod vswitch ron=4 roff=2meg von=3 voff=2
.subckt comp1 out inp inm vss
e1 out vss table {(v(inp)-v(inm))*5000} = (0,0) (3.2,3.2)
Rout out vss 10meg
Rinp inp vss 10meg
Rinm inm vss 10meg
.ends comp1
.ends EL7182C
*$

*EL7202C Macromodel
*Revision A, 1/23/2004
*Connection     In
*               |       Gnd
*               |       |       Vcc
*               |       |       |       Out
.subckt EL7202C 2       3       6       7 
V1 12 3 1.6
R1 13 15 1k 
R2 14 15 5k  
R5 11 12 100 
C1 15 3 33.3pF
D1 14 13 dmod
X1 13 11 2 3 comp1
X2 16 15 12 3 comp1
sp 6 7 16 3 spmod
sn 7 3 16 3 snmod 
g1 11 0 15 0 -940u
.model dmod d
.model spmod vswitch ron=3 roff=2meg von=1 voff=1.5
.model snmod vswitch ron=4 roff=2meg von=3 voff=2
.ends EL7202C
.subckt comp1 out inp inm vss
e1 out vss table {(v(inp)-v(inm))*5000} = (0,0) (3.2,3.2)
Rout out vss 10meg
Rinp inp vss 10meg
Rinm inm vss 10meg
.ends comp1
*$

*EL7212C Macromodel
*Revision B, August 1997
*Connection         In
*                         |       Gnd
*                         |        |       Vcc
*                         |        |        |       Out
.subckt EL7212C 2       3       6       7 
V1 12 3 1.6
R1 13 15 1k 
R2 14 15 5k  
R5 11 12 100 
C1 15 3 33.3pF
D1 14 13 dmod
X1 13 11 2 3 comp1
X2 16 12 15 3 comp1
sp 6 7 16 3 spmod
sn 7 3 16 3 snmod 
g1 11 0 15 0 -940u
.model dmod d
.model spmod vswitch ron=3 roff=2meg von=1 voff=1.5
.model snmod vswitch ron=4 roff=2meg von=3 voff=2
.subckt comp1 out inp inm vss
e1 out vss table {(v(inp)-v(inm))*5000} = (0,0) (3.2,3.2)
Rout out vss 10meg
Rinp inp vss 10meg
Rinm inm vss 10meg
.ends comp1
.ends EL7212C
*$

* EL7242C Spice Model   
*Revision A, January 1996
*Connection     In+
*               |       In-
*               |       |       Gnd
*               |       |       |       Out
*               |       |       |       |       Vcc
.subckt EL7242C 1       2       5       7       8 
V1 12 5 1.6
R1 13 15 1k 
R2 14 15 5k  
R5 11 12 100 
C1 15 5 43.3pF
D1 14 13 dmod
X1 13 11 1 5 comp1
X2 16 12 15 5 comp1
V2 22 5 1.6
R6 23 25 1K
R7 24 25 5K
R8 21 22 100
C2 25 5 43.3pF
D2 24 23 dmod
X3 23 21 2 5 comp1
X4 26 25 22 5 comp1
X5 16 26 17 5 And-gate
sp 8 7 17 5 spmod
sn 7 5 17 5 snmod 
g1 11 5 13 5 938u
g2 21 5 23 5 938u
.model dmod d
.model spmod vswitch ron=3 roff=2meg von=1 voff=1.5
.model snmod vswitch ron=4 roff=2meg von=3 voff=2
.subckt And-gate inp1 inp2 out-A Vss-A
e1 out-A Vss-A table {v(inp1)*v(inp2)} = (0,3.2) (3.2,0)
Rout-a out-a vss-a 10meg
rinpa inp1 vss-a 10meg
rinpb inp2 vss-a 10meg
.ends and-gate
.subckt comp1 out inp inm vss
e1 out vss table {(v(inp)-v(inm))*5000} = (0,0) (3.2,3.2)
Rout out vss 10meg
Rinp inp vss 10meg
Rinm inm vss 10meg
.ends comp1
.ends EL7242C
*$

*EL7457C Spice Model
*Revision A, March 2002
*This spice model only models one channel.  
*Connection	INA
*		|	OE
*		|	|	VL
*		|	|	|	GND
*		|	|	|	|	VS-
*		|	|	|	|	|	VH
*		|	|	|	|	|	|	OUTA
*		|	|	|	|	|	|	|	VS+
*		|	|	|	|	|	|	|	|
.subckt EL7457C	1	2	4	5	9	12	15	16
*channel A
ca1 1 5 3.5pF
ra1 1 5 10Meg
spa1 17 16 1 5 swp
spa2 17 9 5 1 swp1
*
* OE pin input
c2 2 5 3.5p 
r2 2 5 10Meg
*
e1a 18 5 17 5 1
ra3 18 5 10k
spa3 18 19 2 5 swp
ra4 19 5 10k
spa4 15 12 19 5 swn
spa6 18 20 2 5 swp
ra6 20 16 10k
ra5 20 5 10k
spa5 15 4 5 20 swp1
d1a 15 12 den
d2a 15 16 den
d3 9 5 den
d4 9 4 den
cla 15 5 80pF
*
* switch model
.model swp vswitch ron=4 roff=10Meg von=2.4 voff=2
.model swn vswitch ron=4 roff=10Meg von=5 voff =4
.model swp1 vswitch ron=4 roff=10Meg von=0 voff=-1
.model den D (n=1)
.ends EL7457C
*$
*
*EL7144E;Power Mosfet Driver;
* EL7144 Spice Model   
*Revision A, February 1996
*Connection 
*                   In+
*                   |      In-
*                   |      |      Gnd
*                   |      |      |      Outn
*                   |      |      |      |     outp
*                   |      |      |      |     |     Vcc
.subckt EL7144E     2      3      5      6     7      8 
V1 12 5 1.6
R1 13 15 1k 
R2 14 15 5k  
R5 11 12 100 
C1 15 5 43.3pF
D1 14 13 dmod
X1 13 11 2 5 comp1
X2 16 12 15 5 comp1
V2 22 5 1.6
R6 23 25 1K
R7 24 25 5K
R8 21 22 100
C2 25 5 43.3pF
D2 24 23 dmod
X3 23 21 3 5 comp1
X4 26 25 22 5 comp1
X5 16 26 17 5 And-gate
sp 8 7 17 5 spmod
sn 6 5 17 5 snmod 
g1 11 5 13 5 938u
g2 21 5 23 5 938u
.model dmod d
.model spmod vswitch ron=3 roff=2meg von=1 voff=1.5
.model snmod vswitch ron=4 roff=2meg von=3 voff=2
.ends 
*
.subckt And-gate inp1 inp2 out-A Vss-A
e1 out-A Vss-A table{(v(inp1)*v(inp2))} = (0, 3.2 ) (3.2, 0)
Rout-a out-a vss-a 10meg
rinpa inp1 vss-a 10meg
rinpb inp2 vss-a 10meg
.ends 
*
.subckt comp1 out inp inm vss
e1 out vss table{(v(inp)-v(inm))*5000} = (0, 0) (3.2, 3.2) 
Rout out vss 10meg
Rinp inp vss 10meg
Rinm inm vss 10meg
.ends 
*
*EL7158I Spice Model
*Revision A, Septmeber 23, 2003
*Connection	Vs+
*		|	OE
*		|	|	VIN
*		|	|	|	GND
*		|	|	|	|	VS-
*		|	|	|	|	|	VL
*		|	|	|	|	|	|	OUT
*		|	|	|	|	|	|	|	VH
*	  	|	|	|	|	|	|	|	|
.subckt EL7158I	1	2	3	4	5	6	7	8
c1 3 4 3.5pF
r1 3 4 50Meg
sp1 11 1 3 4 swp
rp1 11 4 10K
*
* OE pin input
c2 2 4 3.5p 
r2 2 4 50Meg
*
*Voutp path
e1 10 4 11 4 1
sp3 10 13 2 4 swp
r5 10 4 1k
r3 13 1 10K
sp9 7 8 4 13 swp1
r7 13 4 10k
d1 5 7 den
d3 7 1 den
clh 7 4 200pF
*
*VoutL path
e2 9 4 11 4 1
sp4 9 14 2 4 swp
r4 14 4 10K
r6 9 4 1k
sp10 6 7 14 4 swn
d2 5 6 den
*
* switch model
.model swp vswitch ron=0.5 roff=10Meg von=2 voff=1.5
.model swn vswitch ron=0.5 roff=10Meg von=5 voff =4
.model swp1 vswitch ron=0.5 roff=10Meg von=0 voff=-1
.model den D (n=1)
.ends EL7158I
*$

