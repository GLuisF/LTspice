*$
**************** Power Discrete MOSFET Electrical Circuit Model *****************
** Product Name: FQD2N60C
** 600V N-Channel QFET MOSFET
** Model Type: BSIM3V3
**-------------------------------------------------------------------------------
.SUBCKT FQD2N60C 2 1 3    
*Nom Temp=25 deg C
Dbody 7 5    DbodyMOD
Dbreak 5 11  DbreakMOD
Ebreak 11 7 17 7 660
Lgate 1 9    1.858e-9
Ldrain 2 5   1.858e-9
Lsource 3 7  0.966e-9
RLgate 1 9   18.58
RLdrain 2 5  18.58
RLsource 3 7 9.66
Rgate 9 6    0.5
It 7 17      1
Rbreak 17 7  RbreakMOD 1
.MODEL RbreakMOD RES (TC1=1.15e-3 TC2=-2.04e-6)
.MODEL DbodyMOD D (IS=1.55e-13 N=1      RS=2.20e-2  TRS1=3.1e-3 TRS2=1.0e-5 
+ CJO=3.01e-10     M=0.36      VJ=0.45  TT=2.12e-7  XTI=3       EG=1.12)
.MODEL DbreakMOD D (RS=100e-3 TRS1=1.0e-3 TRS2=1e-6)
Rdrain 5 16 RdrainMOD 3.6
.MODEL RdrainMOD RES (TC1=8.12e-3 TC2=1.95e-5)
M_BSIM3 16 6 7 7 Bsim3 W=0.406 L=2.0e-6 NRS=1
.MODEL Bsim3 NMOS (LEVEL=7 VERSION=3.1 MOBMOD=3 CAPMOD=2 PARAMCHK=1 NQSMOD=0
+ TOX=780e-10    XJ=1.4e-6      NCH=1.7e17    
+ U0=700         VSAT=1.0e5     DROUT=1.0      
+ DELTA=0.1      PSCBE2=0       RSH=1.2e-4 
+ VTH0=3.05      VOFF=-0.1      NFACTOR=1.1     
+ LINT=1.16e-7   DLC=1.16e-7    FC=0.5
+ CGSO=1.01e-15  CGSL=0         CGDO=1.05e-13  
+ CGDL=6.62e-10  CJ=0           CF=0 
+ CKAPPA=0.2     KT1=-1.47      KT2=0
+ UA1=-3.02e-9   NJ=10)
.ENDS
*$
*************** Power Discrete MOSFET Thermal Model ********************
** Package: D-PAK
**----------------------------------------------------------------------
.SUBCKT FQD2N60C_Thermal TH TL
CTHERM1 TH 6 2.15e-05
CTHERM2 6 5  1.64e-04
CTHERM3 5 4  2.02e-03
CTHERM4 4 3  9.16e-03
CTHERM5 3 2  3.83e-02
CTHERM6 2 TL 8.02e-02 
RTHERM1 TH 6 4.38e-03
RTHERM2 6 5  1.88e-02
RTHERM3 5 4  9.91e-02
RTHERM4 4 3  5.32e-01
RTHERM5 3 2  6.90e-01
RTHERM6 2 TL 1.52e+00
.ENDS FQD2N60C_Thermal
**-------------------------------------------------------------------------------
** Creation: Apr.-18-2016   Rev.: 0.0
** Fairchild Semiconductor
*$

