* LT1360 SPICE Macro-model               
*
* Revision History:
* 
* Node assignments
*              non_inverting input
*              |    inverting input
*              |    |    positive supply
*              |    |    |   negative supply
*              |    |    |    |     output
*              |    |    |    |     |
.subckt LT1360 1 2 3 4 5
A1 2 1 0 0 0 0 0 0 OTA g=0 in=.9p ink=90
C2 2 1 1p Rpar=5Meg noiseless
B1 0 N004 I=10u*dnlim(uplim(V(1),V(3)-1.4,.1), V(4)+1.6, .1)+1n*V(1)
B2 N004 0 I=10u*dnlim(uplim(V(2),V(3)-1.39,.1), V(4)+1.59, .1)+1n*V(2)
C9 3 2 1.5p Rpar=100Meg noiseless
C10 N004 0 7f Rpar=100K noiseless
M1 3 N005 5 5 N temp=27
M2 4 N005 5 5 P temp=27
C3 3 5 1p
D5 N005 5 YU
D6 5 N005 YD
R2 3 N006 42Meg noiseless
R3 N006 4 42Meg noiseless
C13 N006 5 2p Rser=5k noiseless
C4 5 4 1p
C1 2 4 1.5p Rpar=100Meg noiseless
C6 3 1 1.5p Rpar=100Meg noiseless
C7 1 4 1.5p Rpar=100Meg noiseless
A6 0 N004 0 0 0 0 N007 0 OTA g=1u linear Cout=1.3f en=9n enk=110 Vhigh=1e308 Vlow=-1e308
D2 2 4 DBIAS
D4 1 4 DBIAS
A3 0 N007 0 0 0 0 N006 0 OTA g=534u iout=10m Cout=1.7p Vlow=-1e308 Vhigh=1e308
D1 N007 0 DSlewLim
G1 0 N005 N006 0 1u
C8 0 N005 1f Rpar=1Meg noiseless
D3 3 4 DC
C11 N006 5 30p Rser=200k noiseless
G2 N006 0 N006 3 100m dir=1 vto=-.814
G3 0 N006 4 N006 100m dir=1 vto=-.814
.model YU D(Ron=100 Roff=1T Vfwd=.805 epsilon=.1 noiseless)
.model YD D(Ron=100 Roff=1T Vfwd=.785 epsilon=.1 noiseless)
.model N VDMOS(Vto=-130m Kp=120m)
.model P VDMOS(Vto=130m Kp=120m pchan)
.model DBIAS D(Ron=1Meg Roff=1G Vfwd=.6 ilimit=80n epsilon=500m noiseless)
.model DSlewLim D(Ron=550k Roff=1Meg Vfwd=1 Vrev=1 epsilon=.1 revepsilon=.1 noiseless)
.model DC D(Ron=2k Roff=1G Vfwd=1 epsilon=.1 ilimit=2.99m noiseless)
.ends LT1360
*
.subckt LT1363 1 2 3 4 5
A1 2 1 0 0 0 0 0 0 OTA g=0 in=1p ink=200
C2 2 1 .1p Rpar=5Meg noiseless
B1 0 N004 I=10u*dnlim(uplim(V(1),V(3)-1.4,.1), V(4)+1.6, .1)+1n*V(1)
B2 N004 0 I=10u*dnlim(uplim(V(2),V(3)-1.39,.1), V(4)+1.59, .1)+1n*V(2)
C9 3 2 1.5p Rpar=100Meg noiseless
C10 N004 0 6f Rpar=100K noiseless
M1 3 N005 5 5 N temp=27
M2 4 N005 5 5 P temp=27
C3 3 5 1p
D5 N005 5 YU
D6 5 N005 YD
R2 3 N006 80Meg noiseless
R3 N006 4 80Meg noiseless
C13 N006 5 20p Rser=200k noiseless
C4 5 4 1p
C1 2 4 1.5p Rpar=100Meg noiseless
C6 3 1 1.5p Rpar=100Meg noiseless
C7 1 4 1.5p Rpar=100Meg noiseless
A6 0 N004 0 0 0 0 N007 0 OTA g=1u linear Cout=1.5f en=9n enk=250 Vhigh=1e308 Vlow=-1e308
D2 2 4 DBIAS
D4 1 4 DBIAS
A3 0 N007 0 0 0 0 N006 0 OTA g=225u iout=5m Cout=512f Vlow=-1e308 Vhigh=1e308
D1 N007 0 DSlewLim
G1 0 N005 N006 0 1u
C8 0 N005 .6f Rpar=1Meg noiseless
D3 3 4 DC
C11 N006 5 800f Rser=4k noiseless
G2 N006 0 N006 3 100m dir=1 vto=-.811
G3 0 N006 4 N006 100m dir=1 vto=-.635
.model YU D(Ron=100 Roff=1T Vfwd=.755 epsilon=.1 noiseless)
.model YD D(Ron=100 Roff=1T Vfwd=1.2 epsilon=.1 noiseless)
.model N VDMOS(Vto=-180m Kp=230m)
.model P VDMOS(Vto=150m Kp=120m pchan)
.model DBIAS D(Ron=100k Roff=1G Vfwd=.6 ilimit=.6u noiseless)
.model DSlewLim D(Ron=410k Roff=1Meg Vfwd=1 Vrev=1 epsilon=.2 revepsilon=.2 noiseless)
.model DC D(Ron=2k Roff=1G Vfwd=1 epsilon=.1 ilimit=4.103m noiseless)
.ends LT1363
*
