***********************************************************************
* 				Thyristor PSpice Models               *
***********************************************************************
* Note :
*
*	This thyristor model simulates:
*	-Igt MAX of the specification
*       -Il  Typ of the specification
*       -Ih  Typ of the specification
*       -VDRM
*       -VRRM
*       -Power dissipation is realistic and correspond to a typical
*        triac
*
*       All these parameters are constant, and don't vary neither 
*       with temperature nor other parameters.
*    
*
*
*$
.subckt SCR_ST A K G PARAMS:
+ Vdrm=400v     
+ Igt=20ma
+ Ih=6ma        
+ Rt=0.01
*
* Vdrm : Repetitive forward off-state voltage
* Ih   : Holding current 
* Igt  : Gate trigger current 
* Rt   : Dynamic on-state resistance
*
*****************
* Power circuit *
*****************
*
****************
*Switch circuit*
****************
* Q1 & Q2 Conduction
S_S3         A Plip1 positive 0 Smain
*RS_S3        positive 0 1G
D_DAK1         Plip1 Plip2 Dak 
R_Rlip         Plip1 Plip2  1k
V_Viak         Plip2 K DC 0 AC 0 
*
**************
*Gate circuit*
**************
R_Rgk         G K  10G  
D_DGKd         G Pg2 Dgk 
V_Vig         Pg2 K DC 0 AC 0 
R_Rlig         G Pg2  1k  
*******************
*Interface circuit*
*******************
* positive pilot
R_Rp         Controlp positive  2.2  
C_Cp         0 positive  1u 
E_IF15OR3         Controlp 0 VALUE {IF(  ( (V(CMDIG)>0.5) | (V(CMDILIH)>0.5) |
+ (V(CMDVdrm)>0.5) ),400,0 )}
* 
******************
* Pilots circuit *
******************
******************
* Pilot Gate     *
******************
E_IF1IG         CMDIG 0 VALUE {IF(  ( I(V_Vig) ) > Igt ,1,0 )}
*
******************
* Pilot IHIL     *
******************
*
E_IF10IL         inIL 0 VALUE {IF( ((I(V_Viak))>2.5*Igt),1,0 )}
E_IF5IH         inIH 0 VALUE {IF( ((I(V_Viak))>(Ih/3)),1,0 )} 
*
* Flip_flop IHIL
E_IF6DIHIL         SDIHIL 0 VALUE {IF(
+ (V(inIL)*V(inIH)+V(inIH)*(1-V(inIL))*(V(CMDILIH)) )>0.5,1,0)}
C_CIHIL         CMDILIH 0  1n  
R_RIHIL         SDIHIL CMDILIH  1K  
R_RIHIL2         CMDILIH 0  100Meg  
******************
* Pilot VDRM     *
******************
E_IF8Vdrm         inVdrm 0 VALUE {IF( (ABS(V(A)-V(K))>(Vdrm*1.3)),1,0 )}
E_IF9IHVDRM         inIhVdrm 0 VALUE {IF( I(V_Viak)>(Vdrm*1.3)/1.2meg
+ ,1,0)}
* Flip_flop VDRM
E_IF7DVDRM         SDVDRM 0 VALUE {IF(
+ (V(inVdrm)+(1-V(inVdrm))*V(inIhVdrm)*V(CMDVdrm) )>0.5,1,0)}
C_CVdrm         CMDVdrm 0  1n  
R_RVdrm         SDVDRM CMDVdrm  100  
R_RVdrm2        CMDVdrm 0  100Meg  
****************
* Switch Model *
****************
.MODEL Smain VSWITCH Roff=1.2meg Ron={Rt} Voff=0 Von=100
****************
* Diodes Model *
****************
.MODEL  Dak D( Is=3E-12 Cjo=5pf) 
.MODEL  Dgk D( Is=1E-16 Cjo=50pf Rs=5 Bv=12V Ibv=100uA) 
.ends 
*
*********************************************************************
* 				SCR PSpice Library                  *
*********************************************************************
*********************************************************************
* 				Standard SCRs			    *
*********************************************************************
*
*$

.subckt TXN1012 A K G
X1 A K G SCR_ST params:
+ Vdrm=1000v     
+ Igt=15ma
+ Ih=30ma        
+ Rt=0.036
* 1999 / ST / Rev 0
.ends
*$
.subckt X0202N A K G
X1 A K G SCR_ST params:
+ Vdrm=800v     
+ Igt=200ua
+ Ih=5ma        
+ Rt=0.15
* 1999 / ST / Rev 0
.ends
*$
***********************************************************************
* 				DIAC PSpice Models                    *
***********************************************************************
.SUBCKT DIAC DIAC_IN DIAC_OUT PARAMS:
+Tr=0.342
+Vbo=13.6V
+Delta_V=19V
+Ibo=15uA
*
*Tr:      Rise time (in us)
*Vbo:	    Break over voltage	
*Delta_V: Dynamic breakover voltage
*Ibo:     Breakover current
C_C1         N02098 DIAC_OUT  1u IC=0 
V_IDIAC1         DIAC_IN N01041 DC 0Vdc AC 0Vac 
R_R1         N02098 TRG  1.462*{Tr}  
D_D1         N06161 N01041 DZ19V 
E_ABM1         TRG DIAC_OUT VALUE { IF(ABS(I(V_IDIAC1))>{Ibo},1,0)}
D_D2         N06161 N01060 DZ19V 
D_D3         N10655 N01060 DZ14V 
D_D4         N10655 DIAC_OUT DZ14V 
S_S1         N01041 N01060 N02098 DIAC_OUT _S1
RS_S1        N02098 DIAC_OUT 1G
C_C2         DIAC_IN DIAC_OUT  10p
****************
* Switch Model *
****************
.MODEL        _S1 VSWITCH Roff=1e7 Ron=2.2 Voff=0.1V Von=0.99V
****************
* Diodes Model *
****************
.model DZ14V  	D(Is=3.142f Rs=0.1 Ikf=0 N=1 Xti=3 Eg=1.11 M=.3282
+		Vj=.75 Fc=.5 Isr=1.973n Nr=2 Bv={Vbo} Ibv=.14467 Nbv=1.093
+		Ibvl=.1m Nbvl=1.2722 Tbv1=001433.3u)
*
.model DZ19V D(Is=6.994f Rs=5.612 Ikf=0 N=1 Eg=1.11 M=.2906
+	     Vj=.75 Fc=.5 Isr=2.088n Nr=2 Bv={Delta_V} Ibv=.17098 Nbv=1.2072
+	     Ibvl=2.002m Nbvl=1.1457 Tbv1=888.89u)
*	Vz = 18 @ 14mA, Zz = 37 @ 1mA, Zz = 11 @ 5mA, Zz = 7.9 @ 20mA
.ends   
*
*********************************************************************
* 				Standard DIACs			    *
*********************************************************************
*
*$

.subckt DB3TG DIAC_IN DIAC_OUT
X1 DIAC_IN DIAC_OUT DIAC params:
+Tr=0.342
+Vbo=13.6V
+Delta_V=19V
+Ibo=15uA
* 2000 / ST / Rev 0
.ends
*$
.subckt DB4TG DIAC_IN DIAC_OUT
X1 DIAC_IN DIAC_OUT DIAC params:
+Tr=0.342
+Vbo=21.6V
+Delta_V=19V
+Ibo=15uA
* 2000 / ST / Rev 0
.ends
*$