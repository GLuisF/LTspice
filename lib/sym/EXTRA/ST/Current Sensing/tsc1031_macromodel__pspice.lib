****************************************************************************
*
* WARNING : please consider following remarks before usage
*
* 1) All models are a tradeoff between accuracy and complexity (ie. simulation
*    time).
*
* 2) Macromodels are not a substitute to breadboarding, they rather confirm the
*    validity of a design approach and help to select surrounding component values.
*
* 3) A macromodel emulates the NOMINAL performance of a TYPICAL device within
*    SPECIFIED OPERATING CONDITIONS (ie. temperature, supply voltage, etc.).
*    Thus the macromodel is often not as exhaustive as the datasheet, its goal
*    is to illustrate the main parameters of the product.
*
* 4) Data issued from macromodels used outside of its specified conditions
*    (Vcc, Temperature, etc) or even worse: outside of the device operating
*    conditions (Vcc, Vicm, etc) are not reliable in any way.
*
****************************************************************************
****
***  TSC1031  Spice macromodel subckt 
***  May 2009
****                      
**** CONNECTIONS: 
****           INVERTING INPUT: MEASURED CURRENT EXITS THE SHUNT ON THE VM SIDE
****             |   
****             | NON-INVERTING INPUT: MEASURED CURRENT ENTERS THE SHUNT ON THE VP SIDE 
****             |   |    
****             |   |  OUTPUT VOLTAGE 
****             |   |   |   
****             |   |   |  POSITIVE POWER SUPPLY LINE
****             |   |   |   |   
****             |   |   |   |    NEGATIVE POWER SUPPLY LINE
****             |   |   |   |     |     
****             |   |   |   |     |    GROUND LINE 
****             |   |   |   |     |     |
****             |   |   |   |     |     |    CONNECTION TO THE OUTPUT RESISTOR 
****             |   |   |   |     |     |     |
****             |   |   |   |     |     |     |   GAIN-SELECT PIN 
****             |   |   |   |     |     |     |    |
.SUBCKT TSC1031  VM  VP  OUT VCCP VCCN GNDLINE  A1  SEL
    XIAMP_SR VRG3_SR INBUF VRG3_SR NET0313 0 OPAMP_SR
    IIB_VP VP 0 DC {Iib}
    IIB_VM VM 0 DC 10u
    VREADI_ROUT NET254 NET308 DC 0
    VREADI_RWAKE NET357 DELAY_GEN DC 0
    V63 NET297 NET251 DC {Vd_compensazione}
    VVLIM_LOW_VRG3 NET258 NET259 DC {Vd_compensazione}
    VREADIO VB_3 OUT DC 0
    V62 NET267 NET253 DC {Vd_compensazione}
    VVLIM_HIGH_VRG3 NET273 NET263 DC {Vd_compensazione}
    DILIM_SINK VB_3_SINK VB_3 DIODE_ILIM
    DILIM_SOURCE VB_3 VB_3_SOURCE DIODE_ILIM
    DVLIM_HIGH_VRG3 VRG3 NET273 DIODE_NOVd
    D38 VK1K2 NET267 DIODE_NOVd
    DVLIM_LOW_VRG3 NET259 VRG3 DIODE_NOVd
    D39 NET251 VK1K2 DIODE_NOVd
    C86 VRG3_5 0 {CBW}
    C87 VRG3_6 0 {CBW}
    C84 VRG3_3 0 {CBW}
    C79 VRG3_2 0 {CBW}
    C85 VRG3_4 0 {CBW}
    C_WAKE DELAY_GEN 0 60p
    CBW VRG3 A1 {CBW}
    E_ROUT NET308 NET328 VALUE={ IF( V(VP,VM)>=0  , ( Ro_sink
++(Ro_source - Ro_sink)*1/(1+exp( -alpha_switch_Ro*V(V_Io_val) ) )
+)*I(VreadI_ROUT)  , Ro_OFF*I(VreadI_ROUT) )}
    E67 INBUF 0 VK1K2 0 1.0
    E83 K2_AV100_VAL 0 VALUE={IF( ( V(SEL)>=1.2 ) & ( V(SEL)<=V(Vccp) )
+, K2_Av100 , 0.0 )}
    E_READIO V_IO_VAL 0 VALUE={I(VreadIo)}
    E64 NET0313 0 VCCP 0 1.0
    EOUT NET328 0 VRG3_SR 0 1.0
    E_IIB_VP_VAL_NEG IIB_VP_VAL_NEG 0 VALUE={IF( V(Vsense)<-180m , 
+(56u)*(-180m) , (56u)*V(Vsense) )}
    EILIM_SOURCE VB_3_SOURCE VDEP_SOURCE VB_3 0 1.0
    
*Eldo:    
*    E_RWAKE_VAL RWAKE_VAL 0 TABLE { V(VP,VM) } = ( 2m , 18k ) (3m ,
*+13.04k) (4m , 10.9k) ( 5m , 9.4k ) (7m , 7.65k) ( 8m , 7.18k ) ( 9m ,
*+6.9k ) (10.5m 6.615k) (12m 6.45k) ( 15m , 6.32k ) (17m 6.27k) ( 20m ,
*+6.19k ) ( 25m , 6.055k ) ( 30m , 5.944k ) ( 35m , 5.845k ) ( 40m , 5.76k
*+) ( 50m , 5.62k ) ( 60m , 5.505k ) ( 70m , 5.417k ) ( 80m , 5.345k ) (
*+90m , 5.29k ) ( 100m , 5.25k ) ( 120m , 5.2k ) ( 150m , 5.15k )
*PSpice:
    E_RWAKE_VAL RWAKE_VAL 0 VALUE={ TABLE( V(VP,VM) , 2m , 18k , 3m ,
+ 13.04k , 4m , 10.9k , 5m , 9.4k , 7m , 7.65k , 8m , 7.18k , 9m ,
+ 6.9k , 10.5m , 6.615k , 12m , 6.45k , 15m , 6.32k , 17m , 6.27k , 20m ,
+ 6.19k , 25m , 6.055k , 30m , 5.944k , 35m , 5.845k , 40m , 5.76k ,
+ 50m , 5.62k , 60m , 5.505k , 70m , 5.417k , 80m , 5.345k ,
+ 90m , 5.29k , 100m , 5.25k , 120m , 5.2k , 150m , 5.15k )}

    E_VDEP_SOURCE_2 VAL_VDEP_SOURCE_FILTERED 0
+VALUE={IF(V(val_vdep_source)>=0, 0, V(val_vdep_source))}
    EVIN_WAKE VSENSE_WAKE 0 VALUE={ V(Vsense)*V(waking-up_ctrl) }
    E_WAKE NET352 0 VALUE={IF( V(VP,VM)>0 , 1 , 0 )}
    E_RWAKE NET352 NET357 VALUE={ V(Rwake_val)*I(VreadI_Rwake) }
    E81 NET360 0 VALUE={ V(VRg3_6)*V(K2_Av50_val) +
+V(VRg3_6)*V(K2_Av100_val) }
    E_WAKING-UP_CTRL WAKING-UP_CTRL 0 VALUE={IF( V(delay_gen)>0.99 , 1
+, 0 )}
    EILIM_SINK VB_3_SINK VDEP_SINK VB_3 0 1.0
    E_VDEP_SINK_3 VDEP_SINK 0 VALUE={IF( abs(I(VreadIo))<1m , 0 ,
+V(val_vdep_sink_filtered))}
    EVIN VSENSE 0 VALUE={ V(VP,VM)*V(check_Vicm)*V(check_Supply) }
    E_VDEP_SOURCE_3 VDEP_SOURCE 0 VALUE={IF( abs(I(VreadIo))<1m , 0 ,
+V(val_vdep_source_filtered))}
    EVLIM_HIGH_VRG3 NET263 0 VCCP 0 1.0
    E79 CHECK_SUPPLY_DUAL 0 VALUE={IF( ( (V(Vccp)>=2.7) & (V(Vccp)<=5.5)
+& (V(Vccn)<=0) & (V(Vccn)>=-8) ) | ( (V(Vccp)>=2.7) & (V(Vccp)<=3.0) &
+(V(Vccn)<=0) & (V(Vccn)>=-11) ) , 1.0 , 0)}
    E_VDEP_SINK_2 VAL_VDEP_SINK_FILTERED 0
+VALUE={IF(V(val_vdep_sink)<=0 , 0 , V(val_vdep_sink))}
    E78 CHECK_SUPPLY_SINGLE 0 VALUE={IF( (V(Vccp)>=2.7) &
+(V(Vccp)<=5.5) , 1.0 , 0)}
    EVLIM_LOW_VRG3 NET258 0 VCCN 0 1.0
    E75 CHECK_VICM 0 VALUE={IF( (V(VP)>=( Vicm_LOW +
+V(Vccn)*V(dual_supply) )) & (V(VP)<=( Vicm_HIGH + V(Vccn)*V(dual_supply)
+)) , 1.0 , 0)}
    E76 CHECK_SUPPLY 0 VALUE={IF( V(dual_supply)>0.9 ,
+V(check_Supply_dual) , V(check_Supply_single) )}
    E_VDEP_SOURCE_1 VAL_VDEP_SOURCE 0 VALUE={ 129.5  -5000*I(VreadIo)}
    EDUAL_SUPPLY DUAL_SUPPLY 0 VALUE={IF( (V(Vccn)==V(GNDLINE)) &
+(V(GNDLINE)==0) , 0 , 1)}
    E82 K2_AV50_VAL 0 VALUE={IF( ( V(SEL)>=-0.3 ) & ( V(SEL)<=0.5 ) ,
+K2_Av50 , 0.0 )}
    E_VDEP_SINK_1 VAL_VDEP_SINK 0 VALUE={ -129.5 -5000*I(VreadIo)}
    E100 NET296 NET297 VALUE={ IF(I(VreadIo)<0 , 49.0*I(VreadIo) , 0 )
+}
    E98 NET304 NET253 VALUE={ 58.0*I(VreadIo) }
    E101 NET296 0 VCCN 0 1.0
    E99 NET304 0 VCCP 0 1.0
    E_IIB_VM_VAL IIB_VM_VAL 0 VALUE={IF( V(Vsense)<= -160m , 
+(-56u)*(-160m) , (-56u)*V(Vsense) )}
    RSEL 0 SEL  {RSEL}
    R144 0 VRG3_6 {Rg3}
    R136 0 VRG3_2 {Rg3}
    R142 0 VRG3_4 {Rg3}
    R143 0 VRG3_5 {Rg3}
    R141 0 VRG3_3 {Rg3}
    RO2_2 NET254 VB_3 {Ro2_2}
    R1 A1 VRG3 {Rg3}
    R147 NET360 VK1K2 1e-3      
    CK1K2 VK1K2 0 10n        
    G_ICC_VSENSE VCCP VCCN VALUE={IF( V(Vsense)>0 , 0.14625E-4 +
+0.0013075*V(Vsense) , 0 )}
    G70 0 VRG3_6 VRG3_5 0 {1/Rg3}
    G67 0 VRG3_3 VRG3_2 0 {1/Rg3}
    G68 0 VRG3_4 VRG3_3 0 {1/Rg3}
    G62 0 VRG3_2 VRG3 0 {1/Rg3}
    G69 0 VRG3_5 VRG3_4 0 {1/Rg3}
    G_IIB-VP_VSENSE VP 0 VALUE={IF( V(Vsense)>0 , V(Vsense)/Rg1 ,
+V(Iib_VP_val_neg) )}
    G60 VM 0 VALUE={IF( V(Vsense)>0 , 0 , V(Iib_VM_val) )}
    G_IOUT_SOURCED VCCP 0 VALUE={IF(I(VreadIo)>0, I(VreadIo),0)}
    GM1 0 VRG3 VALUE={IF( V(VP,VM)>=0 , V(Vsense_wake)/Rg1 , 0 )}
    G_ICC_VCC VCCP VCCN POLY(1) VCCP 0 2.4565217391304354E-4 
+1.0869565217391281E-5
    G_IOUT_SINKED VCCN 0 VALUE={IF(I(VreadIo)>0, 0, I(VreadIo))}
.ENDS
*** End of subcircuit definition.


*******************************************************************************
*
* MODELS/SUBCKTS and PARAMS used by TSC1031 subckt:
*

.SUBCKT OPAMP_SR VM VP VS VCCP VCCN
    M_NMOS2 VO_DIFF_MINUS VM VEE_N VCCN_ENHANCED MOS_N L={L} W={W}
    M_NMOS1 VO_DIFF_PLUS VP VEE_N VCCN_ENHANCED MOS_N L={L} W={W}
    IEE_N VEE_N VCCN_ENHANCED DC {IEE}
    VVLIM_LOW_VB NET0109 NET0110 DC {Vd_compensazione}
    VPROT_IN_P_VCCP NET0123 NET0134 DC {V_DPROT}
    V_ENHANCE_VCCN VCCN_ENHANCED VCCN DC {VCCN_enhance}
    VVLIM_HIGH_VB NET0187 NET0153 DC {Vd_compensazione}
    V_ENHANCE_VCCP VCCP_ENHANCED VCCP DC {VCCP_enhance}
    V_OUTVLIM_LOW NET0224 NET125 DC {Vd_compensazione}
    VPROT_IN_M_VCCN NET0116 NET0192 DC {V_DPROT}
    V_OUTVLIM_HIGH NET0201 NET0131 DC {Vd_compensazione}
    VPROT_IN_P_VCCN NET0115 NET096 DC {V_DPROT}
    VPROT_IN_M_VCCP NET0190 NET0135 DC {V_DPROT}
    DVLIM_HIGH_VB VB NET0187 DIODE_NOVd
    DPROT_IN_M_VCCP VM NET0135 DIODE_VLIM
    DVLIM_LOW_VB NET0110 VB DIODE_NOVd
    DPROT_IN_M_VCCN NET0116 VM DIODE_VLIM
    D_OUTVLIM_LOW NET125 VB_3 DIODE_NOVd
    DPROT_IN_P_VCCP VP NET0134 DIODE_VLIM
    DPROT_IN_P_VCCN NET0115 VP DIODE_VLIM
    D_OUTVLIM_HIGH VB_3 NET0201 DIODE_NOVd
    CCOMP VB VB_2 {Ccomp}
    EMEAS_VOUT_DIFF VOUT_DIFF 0 VO_DIFF_PLUS VO_DIFF_MINUS 1.0
    EVLIM_HIGH_VB NET0153 0 VCCP 0 1.0
    EVLIM_HIGH_VOUT NET0131 0 VCCP 0 1.0
    EVLIM_LOW_VB NET0109 0 VCCN 0 1.0
    E2_REF NET0238 0 VCCN 0 1.0
    E_VREF VREF 0 NET0250 0 1.0
    E1_REF NET0210 0 VCCP 0 1.0
    EVLIM_LOW_VOUT NET0224 0 VCCN 0 1.0
    RO2_2 VB_3 VB_2 {Ro2_2}
    RPROT_IN_P_VCCP NET0123 VCCP {RPROT_VCCP}
    RPROT_IN_M_VCCP VCCP NET0190 {RPROT_VCCP}
    RO1 VS VB_3 {Ro1}
    RD1 VCCP_ENHANCED VO_DIFF_PLUS {RD}
    RD2 VCCP_ENHANCED VO_DIFF_MINUS {RD}
    RO2_1 VREF VB_2 {Ro2_1}
    R1_REF NET0210 NET0250 1Meg
    R1 VB VREF {R1}
    RPROT_IN_M_VCCN VCCN NET0192 {RPROT_VCCN}
    R2_REF NET0250 NET0238 1Meg
    RPROT_IN_P_VCCN NET096 VCCN {RPROT_VCCN}
    G_I_VB VB_2 VREF VB VREF {GB}
    GM1 VREF VB VOUT_DIFF 0 {1/RD}
.ENDS
*** End of subcircuit definition.

.PARAM RSEL = 5.5e6
.PARAM Vicm_LOW  = 2.9
.PARAM Vicm_HIGH = 75
.PARAM Iib = 10e-6
.PARAM Rg1 = 5k
.PARAM K1       = 25
.PARAM Rg3      = {K1*Rg1}
.PARAM K2_Av50  = 2
.PARAm K2_Av100 = 4 
.PARAM Ro_sink = 2
.PARAM Ro_source = 2
.PARAM Ro_off = 1.05
.PARAM alpha_switch_Ro = 1e4
.PARAM CBW = 0.533p
.PARAM RD=1k
.PARAM VCCP_enhance=150m
.PARAM VCCN_enhance=-1100m 
.PARAM Ccomp=11p 
.PARAM IEE=10u
.PARAM A0=97.93103448E3
.PARAM Ro=17587.2
.PARAM W=11u
.PARAM L=1u
.PARAM gm_mos=0.0002347956532101469
.PARAM GB=10m  
.PARAM Ro1=1
.PARAM Ro2_2=1e-3
.PARAM Ro2_1={Ro - Ro2_2 - Ro1}  
.PARAM R1={A0/(gm_mos*GB*Ro2_1)}
.PARAM V_DPROT=150m
.PARAM RPROT_VCCP=100
.PARAM RPROT_VCCN=15k
.PARAM Vd_compensazione=-788.4u

*Eldo:
*.MODEL MOS_N  NMOS LEVEL=1 MODTYPE=ELDO VTO=+0.65  KP=500E-6
*.MODEL DIODE_NOVd D LEVEL=1 MODTYPE=ELDO IS=10E-15 N=0.001
*.MODEL DIODE_VLIM D LEVEL=1 MODTYPE=ELDO IS=0.8E-15  
*.MODEL DIODE_ILIM D LEVEL=1 MODTYPE=ELDO IS=0.8E-15
*.MODEL DX D LEVEL=1 MODTYPE=ELDO IS=1E-14 
*PSpice:
.MODEL MOS_N  NMOS LEVEL=1 VTO=+0.65  KP=500E-6
.MODEL DIODE_NOVd D LEVEL=1 IS=10E-15 N=0.001
.MODEL DIODE_VLIM D LEVEL=1 IS=0.8E-15  
.MODEL DIODE_ILIM D LEVEL=1 IS=0.8E-15
.MODEL DX D LEVEL=1 IS=1E-14 
*
*******************************************************************************
