* WARNING : please consider following remarks before usage
*
* 1) All models are a tradeoff between accuracy and complexity (ie. simulation 
*    time).
* 2) Macromodels are not a substitute to breadboarding, they rather confirm the
*    validity of a design approach and help to select surrounding component values.
*
* 3) A macromodel emulates the NOMINAL performance of a TYPICAL device within 
*    SPECIFIED OPERATING CONDITIONS (ie. temperature, supply voltage, etc.).
*    Thus the macromodel is often not as exhaustive as the datasheet, its goal
*    is to illustrate the main parameters of the product.
*
* 4) Data issued from macromodels used outside of its specified conditions
*    (Vcc, Temperature, etc) or even worse: outside of the device operating 
*    conditions (Vcc, Vicm, etc) are not reliable in any way.
*
* -------------------------------------------------------------------------
*
** Standard Linear Ics Macromodels
** CONNECTIONS :
* 1 INVERTING INPUT
* 2 NON-INVERTING INPUT
* 3 OUTPUT
* 4 POSITIVE POWER SUPPLY
* 5 NEGATIVE POWER SUPPLY
*
.SUBCKT TS94X 1 2 3 4 5
********************************************************** 
.MODEL MDTH D IS=1E-8 KF=2.626433E-33 CJO=10F
.MODEL NDTH D IS=1E-12
* INPUT STAGE
CIP 2 5 1.000000E-12
CIN 1 5 1.000000E-12
EIP 10 5 2 5 1
EIN 16 5 1 5 1
RIP 10 11 2.600000E+01
RIN 15 16 2.600000E+01
RIS 11 15 1.433696E+02
DIP 11 12 MDTH 400E-12
DIN 15 14 MDTH 400E-12
VOFP 12 13 DC 0.000000E+00
VOFN 13 14 DC 0
IPOL 13 5 1.000000E-05
CPS 11 15 7.404465E-09
DINN 17 13 MDTH 400E-12
VIN 17 5 -0.5000000e+00
DINR 15 18 MDTH 400E-12
VIP 4 18 2.1
FCP 4 5 VOFP 0.1
FCN 5 4 VOFN 0.1
* AMPLIFYING STAGE
FIP 5 19 VOFP 7.000000E+00
FIN 5 19 VOFN 7.000000E+00
DOPM 51 22 MDTH 400E-12
DONM 21 52 MDTH 400E-12
HOPM 22 28 VOUT 72500
VIPM 28 4 1.500000E+02
HONM 21 27 VOUT 33333
VINM 5 27 1.500000E+02
CC 19 29 11.900000E-09
HZTP 30 29 VOFP 8.414314E+02
HZTN  5 30 VOFN 8.414314E+02
DPOS 19 53 NDTH 400E-12
VPOS 51 53 0.75
DNEG 54 19 NDTH 400E-12
VNEG 54 52 0.75
RG11 51 5 8.29E+08
RG12 51 4 8.29E+08
RG21 52 5 1.43E+07
RG22 52 4 1.43E+07
E1 50 40 51 0 1
E2 40 39 52 0 1
EDEC1 38 39 4 0 0.5
EDEC2 0 38 5 0 0.5
RFOL 50 5 1E12
EOUT 26 23 50 5 1
VOUT 23 5 0
ROUT 26 3 100
COUT 3 5 1.000000E-12
DOP 19 25 MDTH 400E-12
VOP 4 25 7.822301E-01
DON 24 19 MDTH 400E-12
VON 24 5 6.722301E-01
.ENDS