*March 3, 2008
*Doc. ID: 69697, S-80401, Rev. A
*File Name: Si8424DB_PS.txt and Si8424DB_PS.lib
*This document is intended as a SPICE modeling guideline and does not
*constitute a commercial product data sheet.  Designers should refer to the
*appropriate data sheet of the same number for guaranteed specification
*limits.
.SUBCKT Si8424DB D G S
M1  3  GX S S NMOS W=1389432u L=0.25u   
M2  S  GX S D PMOS W=1389432u L=0.35u
RG  G  GX     13   
R1  D  3      RTEMP 16E-3
CGS GX S      1160E-12
DBD S  D      DBD
**************************************************************** 
.MODEL  NMOS       NMOS ( LEVEL  = 3            TOX    = 1.3E-8
+ RS     = 8E-3           RD     = 0            NSUB   = 4.53E17  
+ KP     = 4E-5           UO     = 650             
+ VMAX   = 0              XJ     = 5E-7         KAPPA  = 5E-2
+ ETA    = 1E-4           TPG    = 1  
+ IS     = 0              LD     = 0               
+ CGSO   = 0              CGDO   = 0            CGBO   = 0 
+ NFS    = 0.8E12         DELTA  = 0.1)
**************************************************************** 
.MODEL  PMOS       PMOS ( LEVEL  = 3            TOX    = 1.3E-8
+NSUB    = 2.4E17         IS	 = 0            TPG    = -1)   
**************************************************************** 
.MODEL DBD D (CJO=450E-12 VJ=0.38 M=0.20
+FC=0.5 TT=2.66e-08 T_MEASURED=25 BV=8.2 
+RS=1.970e-02 N=1.078 IS=3.559e-09 IKF=1000
+EG=8.436e-01 XTI=1.000e-01 TRS1=1.311e-03 )
**************************************************************** 
.MODEL RTEMP RES (TC1=4.5E-3 TC2=5.5E-6)
**************************************************************** 
.ENDS
